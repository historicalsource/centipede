	.TITLE CENTI
	.INCLUDE CENDEF
	.LIST
	.PAGE
	.LIST
	.ASECT
	.SBTTL MAINLOOP
	.=2000
	JMP RESET

CPYRHT:	.BYTE 1B		;CIRCLE C FOR "COPYRIGHT"
	.ASCIN /1980 ATARI/	;COPYRIGHT MESSAGE

MAIN:	CLI			;ENABLE IRQ
	JSR INIT		;INITIAIZE EVERYTHING
	JSR SCORES		;DISPLAY HIGH SCORES
1$:	LSR SYNC
	BCC 1$			;WAIT FOR IRQ TO GET TO VBLANK
	STA WTCHDG		;WRITE TO WATCHDOG
	LDA SLFTST
	AND I,20
3$:	BEQ 3$			;IN SELF-TEST WAIT FOR WATCHDOG
	JSR CHKST		;CHECK FOR GAME START
	JSR SOUNDS		;PROCESS SOUNDS
	JSR GETINT		;GET INITIAL FOR HIGH SCORE TABLE
	BPL 1$			;IF WE ARE ENTERING INITIALS
	JSR WRITEA		;WRITE AND INITIALIZE EA ROM
	JSR ATTRT		;ATTRACT MODE
	JSR UPSCRE		;UPDATE SCORES AT TOP OF PLAYFIELD
	JSR MOTION		;MOVE CENTIPEDES
	JSR EXPLOD		;EXPLODE SEGMENTS AND PLAYER
	JSR MOVE		;MOVE PLAYER
	JSR BUGMV		;MOVE BUG
	JSR SHOOT		;FIRE SHOTS AND CHECK FOR COLLISIONS
	JSR NEWHD		;IS IT TIME FOR NEW HEADS
	JSR SCORP		;MOVE AND START SCORPION
	JSR ANTMV		;MOVE ART DOWN SCREEN
	JSR CHKEND		;CHECK FOR END OF GAME
	JSR RESTOR		;RESTORE MUSHROOMS
	JMP 1$

CKSUM0:	.BYTE 0C3		;CHECKSUM 2000-27FF
	.PAGE

	.SBTTL ANTMV-MOVE ANT DOWN SCREEN
;ANTMV--MOVE ANT IF FEW MUSHROOMS ON SCREEN
;
;
ANTMV:	LDA PLAYP
	AND I,0AF
	BNE 2$			;DON'T MOVE IF PLAYER IS DEAD
	LDA ANTP
	EOR CKC0
	CMP I,20
	BCC 4$			;MOVE ANT
2$:	RTS			;ALWAYS

4$:	LDA ANTV
	EOR CKF8
	CMP I,0F8
	BCC 5$			;IF ON SCREEN, KEEP MOVING
	LDX PLAYER
	LDA X,CENTIN-1
	CMP I,12.
	BCS 30$			;NO ANTS EARLY IN GAME
	LDA X,SCORE2-1
	CMP I,2
	LDY I,05
	BCC 3$			;MORE ANTS AT HIGHER SCORES
	LDY I,09
3$:	CMP I,12
	BCC 8$			;NOT OVER 120K IN SCORE
	LSR			;EVERY 20K
	CLC
	ADC I,6			;INCREASE MINIMUM NUMBER OF MUSHROOMS
	TAY
8$:	TYA
	CMP X,MUSH-1
	BCC 30$			;IF THERE ARE MANY MUSHROOMS NEAR BOTTOM
5$:	LDA FRAME
	AND I,03
	BNE 20$			;SLOW DOWN PICTURE CHANGES
	INC ANTP
	LDA ANTP
	CLC
	ADC I,01
	AND I,03
	ORA I,1C		;FROM 1C TO 1F
	EOR CKC0
	STA ANTP		;CHANGE PICTURE
20$:	LDA ANTH
	STA TEMP1
	LDA ANTV
	LDY CKC0
	BEQ 25$			;NOT COCKTAIL NOW
	CLC
	ADC ANTDV
	JMP 27$			;ALWAYS

25$:	SEC
	SBC ANTDV
27$:	STA ANTV
	EOR CKF8
	CMP I,4
	BCC 40$			;IF BOTTOM OF SCREEN
	LDX I,12.
	JSR PLAY		;CHECK FOR COLLISION
	BCC 30$			;IF PLAYER IS DEAD
	LDA FRAME
	AND I,03
	BNE 30$			;PUT UP MUSHROOMS ONLY EVERY FOUR FRAMES
	LDA RNGEN
	AND I,03
	BNE 30$			;DO NOT PUT UP MUSHROOM
	LDA I,4			;MAKE MUSHROOM APPEAR AFTER ANT
	EOR CKFF		;4 OR -5 FOR PLAYER 1 OR 2 COCKTAIL
	CLC
	ADC ANTV
	LDY I,0
	JSR OBSTAC
	JSR MUSHER		;PUT MUSHROOMS ON SCREEN
30$:	RTS

40$:	JSR ANTPC		;REMOVE ANT FROM SCREEN
45$:	RTS
	.PAGE
	.SBTTL ANTPC-INITIALIZE ANT
;ANTPC--INITIALIZE ANT PICTURE,ETC.
;
;
ANTPC:	LDA I,1C
	EOR CKC0
	STA ANTP		;PICTURE
	LDA I,0F8
	EOR CKF8		;0 OR F8
	STA ANTV		;VPOS
10$:	LDA RNGEN		;WILL HANG IF POKEY IS GONE AND D0-D7=0'S OR 1'S
	AND I,0F8
	BEQ 10$			;OFF SCREEN
	CMP I,10
	BCC 10$			;OFF SCREEN
	SEC
	SBC I,04		;WE WANT
	STA ANTH		;HPOS
	LDY I,3
	LDX PLAYER
	LDA X,SCORE2-1
	CMP I,6
	BCS 20$			;ANT GOES FASTER A 60K
	LDY I,2
20$:	STY ANTDV		;VDIR
	LDA I,0
	STA ANTDH		;HDIR
	STA CHAN6		;NO SCORPION SOUND
	RTS
	.PAGE
	.SBTTL ATTRACT-MODE GAMEPLAY
;ATTRT-ATTRACT MODE GAMEPLAY
;
ATTRT:	LDA MODE
	BPL 70$			;IF NOT IN ATTRACT
	JSR BONUS		;DISPLAY BONUS LEVEL
ATTRT1 =.			;ADDRESS FOR CHECKSUMMING COPYRIGHT MESSAGE CODE
	LDA I,CPYRHT&0FF
	STA TEMP5
	LDA I,CPYRHT/100
	STA TEMP5+1		;ADDRESS OF MESSAGE
	LDA I,40
	STA TEMP4
	LDA I,5
	STA TEMP4+1		;STARTING POSITION
	JSR MESSAG		;PUT OUT MESSAGE
ATTRT2 =.			;END OF COPYRIGHT MESSAGE CODE
	LDA FRAME
	BNE 7$			;EVERY 4 SECONDS
	LDA I,84
	JSR MESS		;CLEAR "GAME OVER" MESSAGE
7$:	LDA FRAME
	LDX PLYFLD+200		;READ A 1 ("A" IN "ATARI") FROM THE SCREEN
	STX ONE
	AND I,80
	BNE 70$			;DON'T MOVE THIS FRAME
	LDA PLAYP
	AND I,0AF
	BNE 55$			;IF WE ARE DESTROYED
	LDA PLAYH
	LDY I,1
	CMP I,1C
	BCC 10$			;REVERSE DIRECTION
	LDY I,-1
	CMP I,0E4
	BCS 10$			;GO ON
	LDY PLAYDH
10$:	TYA
	STY PLAYDH
	CLC
	JSR MHOR		;MOVE HORIZONTALLY
	LDA PLAYV
	STA TEMP2		;SAVE OLD VERTICAL POSITION FOR "RSHOT1"
	LDY I,-1
	CMP I,30
	BCS 30$			;REVERSE DIRECTION
	LDY I,1
	CMP I,9
	BCC 30$			;GO ON
	LDY PLAYDV
30$:	TYA
	STY PLAYDV
	CLC
	JSR MVER		;MORE VERTICALLY
55$:	JSR RSHOT1		;REPOSITION SHOT
	LDX I,ATTRT2-ATTRT1
	LDA I,0F9		;EXPECTED CHECKSUM
60$:	EOR X,ATTRT1
	DEX
	BPL 60$
	STA MCKSUM		;0 IF COPYRIGHT MESSAGE CODE IS OK
70$:	RTS


	.BYTE 2,0BB,5A,30
	.BYTE 5F,0EE,7D,0A8	;MORSE CODE FOR "COPYRIGHT 1980 ATARI"

	.SBTTL	BONUS - DISPLAY "BONUS LIFE EVERY XXXX" MESSAGE
BONUS:	JSR BONUS1		;GET BONUS INCREMENT
	STA BONUSL
	LDA Y,BONUSV+1
	STA BONUSM
	LDA I,6
	JSR MESS		;DISPLAY "BONUS LIFE EVERY"
	LDA BONUSM
	JSR DIGITZ		;DISPLAY DIGIT
	LDA BONUSL
	JSR DIGIT2		;DISPLAY 2 DIGITS
	LDA I,0
	JMP DIGIT2		;2 TRAILING ZEROS

BONUS1:	LDA OPTNS
	AND I,30		;D4-D5=BONUS LEVEL OPTIONS
	LSR
	LSR
	LSR
	TAY
	LDA Y,BONUSV		;GET BONUS VALUE INCREMENT
	RTS

BONUSV:	.WORD 100,120,150,200	;*100 PER BONUS LIFE
	.PAGE
	.SBTTL	BUGOFF-INITIALIZE BUG PICTURE
;BUGOFF-INITIALIZE BUG PICUTRE, VPOS,HPOS, AND DIRECTIONS
;
;
BUGOFF:	LDX PLAYER
	LDY I,2
	LDA X,SCORE2-1
	BNE 10$			;IF SCORE > 9999
	LDA OPTNS
	AND I,40		;GET DIFFICULTY SWITCH
	ORA I,10		;EITHER 100(HARD) OR 5000 (EASY)
	CMP X,SCORE1-1
	BCC 10$			;IF SCORE < 1000,USE SLOW SPIDER
	LDY I,1				
10$:	STY BUGDV		;V DIR
	LDA RNGEN
	AND I,04
	BEQ 20$			;VARY SIDE SPIDER COMES OUT ON
	TYA
	JSR COMP
	TAY
20$:	STY BUGDH		;SET H DIRECTION
	LDA I,60
	EOR CKF8		;0 OR F8
	STA BUGV		;VPOS
	LDA I,0FF
	STA BUGH		;HPOS
	LDA I,0F8
	STA BUGP
	LDA I,60
	STA COUNT2
	LDA I,0
	STA CHAN3		;TURN OFF SOUND
	RTS
	.PAGE
	.SBTTL	BUGMV-MOVE BUG
;BUGMV-UPDATE POSITION OF BUG
;
;
BUGMV:	LDA PLAYP
	AND I,0AF
	BEQ 5$			;BUG MOVE IF PLAYER IS ALIVE
3$:	RTS

5$:	LDX I,13.		;SET UP INDEX
	LDA BUGP
	TAY
	AND I,20
	BEQ 10$			;NORMAL SPIDER PICTURE
	CPY I,0F8
	BCC 3$			;PTS.STILL ON	
	JMP 130$		;IF BUG IS DEAD

10$:	LDA FRAME
	AND I,03
	BNE 20$			;SLOW DOWN FACE CHANGES
	INC BUGP
	LDA BUGP
	EOR CK40
	CMP I,1C
	BCC 20$			;WITHIN LIMIT
	LDA I,14
	EOR CK40
	STA BUGP		;NEW FACE FOR BUG
20$:	DEC COUNT2
	BNE 40$			;CONTINUE IN SAME DIRECTION
	LDA RNGEN
	AND I,80
	BEQ 30$			;(USE RAND NO. GEN.)DO NOT CHANGE DIRECTION
	LDA BUGDH
	BEQ 24$			;IF HDIR=0
	LDY BUGH
	CPY I,0FB
	BCS 30$			;IF AT LEFT EDGE DON'T CLEAR HDIRECTION
	CPY I,05
	BCC 30$			;IF AT RIGHT EDGE DON'T CLEAR H DIRECTION
	STA OLDDH
	LDA I,0
	BEQ 25$			;ALWAYS

24$:	LDA OLDDH
25$:	STA BUGDH		;STORE NEW HDIR
30$:	LDA OPTNS
	AND I,40		;GET DIFFICULTY SWITCH
	ORA I,20		;EITHER 1/4 OR 1/2 PROBABILITY
	AND RNGEN
	BEQ 35$			;DO NOT CHANGE DIRECTION
	LDA BUGDV
	JSR COMP
	STA BUGDV		;REVERSE V DIRECTION
35$:	LDA I,30
	STA COUNT2		;DELAY CHANGE INDIRECTION
40$:	LDA BUGH
	SEC
	SBC BUGDH
	STA BUGH
	STA TEMP1		;AJUST CALL FOR OBSTAC ROUTINE
	LDA BUGV
	LDY CKC0
	BEQ 50$			;NOT COCKTAIL NOW
	CLC
	ADC BUGDV
	JMP 55$			;ALWAYS

50$:	SEC
	SBC BUGDV
55$:	STA BUGV
	LDY I,0
	JSR OBSTAC		;CHECK FOR PLAYFIELD OBSTACLE
	BEQ 60$
	LDY I,0
	LDA NY,OBST
	AND I,3F
	CMP I,38
	BCC 60$			;DO NOT LET BUG EAT MESSAGES.
	LDA I,0
	STA NY,OBST		;REMOVE OBSTACLE
	JSR MUSHDC		;DECREMENT MUSHROOMS
	LDX I,13.		;RESTORE INDEX
60$:	LDA BUGH
	CMP I,0FF
	BCS 100$		;IF OFF SCREEN
	LDA BUGV
	EOR CKF8
	CMP I,9
	BCS 70$			;IF NOT AT BOTTOM
	LDA BUGDV
	BPL 85$			;CHANGE V DIRECTION
	BMI 80$			;ALWAYS

70$:	LDX PLAYER
	LDA X,SCORE2-1
	SED
	SEC
	SBC I,6
	CLD
	BPL 71$			;IF SCORE >=60K OR >=860K
	LDA I,0			;USE MIN
71$:	LSR			;Y=(SCORE2-6)/2
	CMP I,6
	BCC 72$			;SCORE < 160K
	LDA I,5			;LIMIT AFTER 160K
72$:	ASL
	ASL
	ASL			;X=Y*8
	STA TEMP2
	LDA I,60
	EOR CKF8
	SEC
	SBC TEMP2		;60-X (OR 98-X FOR COCKTAIL)
74$:	LDX CKC0
	BEQ 75$			;NOT COCKTAIL NOW
	CMP BUGV
	BCC 80$			;NOT AT TOP
	BCS 76$			;ALWAYS

75$:	CMP BUGV
	BCS 80$			;NOT AT TOP
76$:	LDA BUGDV
	BMI 85$			;CHANGE V DIRECTION
80$:	LDX I,13.		;RESTORE X
	JSR OVRLAP
	BCC 90$			;NO OVERLAP OF SEGMENTS
	LDA BUGDV
85$:	JSR COMP
	STA BUGDV		;REVERSE V DIRECTION
90$:	LDX I,13.		;RESTORE X
	JSR PLAY
	RTS

100$:	JSR BUGOFF		;KEEP BUG OFF SCREEN TEMPORARILY
	RTS


130$:	DEC COUNT2
	BNE 140$		;IF COUNTER NOT THRU
	LDA RNGEN
	AND I,2F
	ORA I,0F
	STA COUNT2		;DELAY CHANGE IN DIRECTION
	LDA I,14
	STA CHAN3		;TURN ON SOUND
	EOR CK40
	STA BUGP		;PICTURE
140$:	RTS



;CALLS-CALL ARGUMENTS FOR OBSTAC ROUTINE
;
;NOTE-TIS USES MOBJDH OF 1 OR FF
;ENTRY	(X)=INDEX OF CURRENT SEGMENT
;EXIT	(X)=INDEX OF CURRENT SEGMENT
;	(TEMP1)=X,MOBJH
;	(Y)=X,MOBJDH(1 OR FF)
;	(A)=X,MOBJV
;
CALLS:	LDA X,MOBJH
	STA TEMP1
	LDY I,-1
	LDA X,MOBJDH
	BMI 10$
	LDY I,1
10$:	LDA X,MOBJV
	RTS
	.PAGE
	.SBTTL	CENTPC-INITIALIZE CENTIPEDE PICTURE
;CENTPC-INITIALIZE CENTIPEDE PICTURE,HPOS,VPOS, & DIRECTION
CENTPC:	LDX PLAYER
	LDA X,DEAD-1
	BNE 5$			;NO CHANGE IN COLOR OR SPEED UNTIL ALL DEAD
	LDA X,LCOLOR-1
	ORA I,80
	STA X,LCOLOR-1		;TIME TO CHANGE COLORS
	LDA X,CENTIS-1
	CMP I,03
	BCC 5$			;IF CENTIS < 3
	DEC X,CENTIN-1
	BNE 3$			;GO OR IF CENTIN>0
	LDA I,0C
	STA X,CENTIN-1		;LONG CENTI AGAIN
3$:	LDA I,02
	LDY X,SCORE2-1
	CPY I,4
	BCS 4$			;ONLY FAST CENTIPEDES AFTER 40000
	LDA I,01
4$:	STA X,CENTIS-1
5$:	LDA I,03
	STA MOBJP		;HEAD PICTURE
	LDA X,CENTIS-1
	STA MOBJDV
	TAY
	LDA FRAME
	AND I,2
	BNE 10$
	TYA
	JSR COMP
	TAY
10$:	STY MOBJDH		;HDIR=CENTIS*1 OR -1
	LDA I,0F8
	EOR CKF8		;0 R F8
	STA MOBJV		;VPOS
	LDA I,80
	STA MOBJH		;HPOS
	LDA X,CENTIN-1
	STA TEMP1
	CMP I,01
	BEQ 60$			;IF N=1 (NO BODY SEGMENTS)
	LDY I,42
	LDX I,01
20$:	STY X,MOBJP		;BODY PICTURE
	LDA I,0F8
	EOR CKF8		;0 OR F8
	STA X,MOBJV		;VPOS
	LDA X,MOBJDV-1
	STA X,MOBJDV
	LDA X,MOBJDH-1
	STA X,MOBJDH		;DIRECTION
	BPL 30$			;IF POSITIVE
	LDA I,8
	BNE 40$			;ALWAYS

30$:	LDA I,-8
40$:	CLC
	ADC X,MOBJH-1
	STA X,MOBJH		;HPOS
	DEY
	CPY I,3F
	BNE 50$			;UNDERFLOW FOR BODY PICTURE
	LDY I,47		;ALTERNATE BODY SEGMENTS
50$:	INX
	CPX TEMP1
	BCC 20$			;IF (X)<(CENTIN-1)
	LDX PLAYER
	LDA X,CENTIN-1
	CMP I,NCENT
	BEQ 90$			;IF CENTIN=12
60$:	LDA I,0F8		;VPOS=F8
	EOR CKF8		;0 OR F8
	LDX TEMP1
70$:	STA X,MOBJV			;VPOS
	LDA I,0
	STA X,MOBJP
	LDA I,2
	LDY CKFE
	BEQ 80$			;NOT TIME FOR COCKTAIL
	TYA
80$:	STA X,MOBJDV
	BIT RNGEN
	BPL 82$			;IF GOING POSITIVE
	JSR COMP		;TRY OTHER DIRECTION
82$:	STA X,MOBJDH		;DIRECTION
	LDA RNGEN
	AND I,0F8
	STA X,MOBJH		;HPOS
	LDA X,MOBJV
	INX
	CPX I,NCENT
	BCC 70$
90$:	LDA I,NCENT
	LDX PLAYER
	STA X,DEAD-1
	LDA MCKSUM		;SHOULD BE ZERO ELSE SCREW THE WORKS
	STA NEWD		;NEWD=0(NOT TIME FOR PUTTING IN NEW HEAD)
	RTS


	.SBTTL	CHKEND-CHECK FOR DELAYS AND END OF GAME
;
CHKEND:	LDA DELAY
	BNE 5$
1$:	RTS

5$:	LDA MEM+1
	BNE 1$			;WAIT UNTIL MUSHROOMS RESTORED
	DEC DELAY
	BNE 1$			;DON'T DO ANYTHING YET
	LDA SWTCHP
	BEQ 40$			;IF WE ARE NOT SWITCHING PLAYERS
	LDA I,80
	JSR MESS		;CLEAR "PLAYER" MESSAGE
	LDA I,0
	TAY
	STA NY,TEMP4		;CLEAR PLAYER NUMBER
	STA SWTCHP		;CLEAR FLAG
	JMP INIT1		;RESET GUN AND SHOT

40$:	LDA PLAYP
	AND I,0AF
	BNE 50$			;IF GUN IS NOT ALIVE-WE ARE ON NEXT WAVE
45$:	JMP 90$

50$:	JSR INIT1		;INITIALIZE PLAYER AND MISSILE
	LDA MODE
	BPL 55$			;IF IN ATTRACT DO NOT CHECK LIVES
	LDA FRAME+1
	BPL 52$			;EVERY 10 MINUTES
	AND I,7F
	STA FRAME+1		;RESET COUNTER
	JSR SWAP		;SWAP THE SCREEN JUST TO REWRITE ANY MEMORY ERRORS
	JSR SCORES		;DISPLAY HIGH SCORES
52$:	JMP 85$

55$:	LDA LIVES
	ORA LIVES+1
	BNE 70$			;IF GAME NOT OVER
	DEC MODE		;MODE=-1 FOR ATTRACT
	JSR UPDATE		;UPDATE HIGH SCORE TABLE
	LDA CKC0
	BEQ 57$			;NOT COCKTAIL
	LDA UPDFLG+1
	BPL 57$			;IF PLAYER 2 WILL BE ASKED FOR HIS INITIALS
	LDA I,80
	STA CKIND
	JSR CLEAR
	JSR INIT1		;INITIALIZE PLAYER AND MISSILE AFTER COCKTAIL
	JSR SWAP
	LDA UPDFLG
	BPL 57$			;PLAYER 1 WILL ENTER INITIALS
	JSR SCORES		;PUT BACK HIGH SCORE TABLE
57$:	JSR BUGOFF
	JSR CENTPC		;GET CENTIPEDE OFF THE SCREEN
	JSR ANTPC
	LDA I,1
	STA FRAME		;COUNTER FOR CLEARING "GAME OVER" MESSAGE
	LDA I,4
	JSR MESS		;"GAME OVER" MESSAGE
	LDX NPLAYR
	LDA I,0FF
	STA X,STRTL1-1		;TURN OFF START LED
	JSR CKSUM		;CHECKSUM EA ROM BUFFER.
	LDA I,EACKSM-EAROM
	STA ROMA		;STARTING ADDRESS
	LDA I,0
	STA ROMC		;COMPARE/WRITE EA ROM
	RTS

70$:	LDX NPLAYR
	DEX
	BNE 71$			;2 PLAYERS
	JMP 80$			;IF 1 PLAYER GAME CONTINUE

71$:	LDX PLAYER
	LDA X,LIVES-1
	BNE 73$			;ONE PLAYER HAS MORE LIVES
	LDA GOVER
	BNE 72$			;TIME TO REMOVE "GAME OVER" MESSAGE
	INC GOVER
	LDA I,80
	STA DELAY		;DELAY AGAIN
	LDA I,0F9
	STA PLAYP		;REMOVE GUN AND SHOT
	STA SHOTP
	LDA I,4
	JSR MESS		;PUT OUT GAME OVER...
	LDA I,0
	JSR MESS		;PLAYER 1
	LDA PLAYER
	ORA I,20
	JSR CHAR			;PLAYER NUMBER
	RTS

72$:	DEC GOVER
73$:	LDA PLAYER
	EOR I,03		;SWITCH PLAYER NUMBER
	TAX
	LDA X,LIVES-1
	BEQ 80$			;NO LIVES FOR OTHER PLAYER
	STX PLAYER		;SWITCH PLAYERS
	LDA I,80
	AND CKIND
	ORA PLAYER
	STA CKIND		;CORRECT COCKTAIL INDICATOR
	CMP I,82
	BNE 738$		;NOT COCKTAIL NOW
	JSR CKSET
738$:	LDA X,COUNT3-1
	STA COUNT1		;RESET NEW HEAD COUNTER
	LDX PLAYER
	CPX I,1
	BNE 739$		;NOT PLAYER 1
	JSR CLEAR
739$:	JSR SWAP		;SWAP PLAYERS' SCREENS
	LDX PLAYER
	CPX I,2
	BNE 75$			;CONTINUE IF NOT PLAYER 2
74$:	LDA X,LIVES-1
	CMP NLIVES
	BNE 75$			;CONTINUE IF NOT PLAYER 2'S FIRST GAME
	LDA SCORE2+1
	BNE 75$			;IF PLAYER TWO IS ON BONUS LIFE
	LDA I,NCENT
	STA X,DEAD-1		;DO THIS SO COLOR GETS INITIALIZED CORRECTLY
	JSR INITSC		;INITIALIZE SCREEN FOR PLAYER 2
75$:	LDA X,LCOLOR-1
	ORA I,40
	STA X,LCOLOR-1		;INDICATE COLOR CHANGE FOR NEXT PLAYER
	LDA I,0A0
	STA DELAY		;DELAY FOR NEXT PLAYER
	LDA I,0
	JSR MESS		;"PLAYER" MESSAGE
	LDA PLAYER
	ORA I,20
	JSR CHAR		;DISPLAY 1 OR 2
	LDA I,0F9		;REMOVE GUN SO "PLAYER" MESSAGE WILL BE DELETED
	STA PLAYP
	STA SHOTP
	STA SWTCHP		;FLAG FOR SWITCHING PLAYERS
80$:	LDX PLAYER
	DEC X,LIVES-1		;START NEXT LIFE
	JSR DLIVES		;DISLAY LIVES
85$:	JSR BUGOFF
	JSR ANTPC
90$:	JSR CENTPC		;GET NEW CENTIPEDE
	RTS

CLEAR:	LDA MCKSUM		;IT BETTER BE ZERO OTHERWISE SOMEONE
	STA OLDTB		;IS MODIFYING OUR PROGRAM
	STA OLDTB+2		;CLEAR TB FOR NEXT PLAYER
	STA FLIP		;BACK TO PLAYER 1'S CONTROLS
	STA CTBALL
	STA CKBF
	STA CK03
	STA CK3F
	STA CKF8
	STA CKC0
	STA CKE0
	STA CK40
	STA CKFF
	STA CKFE
	STA CKFC
	RTS


CKSET:	LDA I,0F8
	STA CKF8
	LDA I,0FF
	STA CKFF
	LDA I,0FE
	STA CKFE
	LDA I,0FC
	STA CKFC
	LDA I,0E0
	STA CKE0
	LDA I,0C0
	STA CKC0
	LDA I,40
	STA CK40
	LDA I,0BF
	STA CKBF
	LDA I,03
	STA CK03
	LDA I,3F
	STA CK3F
	LDA I,80
	STA FLIP		;PLAYER TWO'S CONTROLS
	STA CTBALL
	LDA I,0
	STA OLDTB
	STA OLDTB+2		;CLEAR TB FOR NEXT PLAYER
	RTS

	.SBTTL	CHKST-CHECK FOR START IF GAME
;CHKST-CHECK FOR START OF GAME
;
CHKST:	LDA OPTSW2
	STA $CMODE		;SET COIN MODE
	AND I,03
	STA TEMP2
	BNE 5$			;IF NOT FREE PLAY
	LDA I,2
	STA $$CRDT		;GIVE 2 CREDITS IN FREE PLAY
5$:	LDA OPTNS
	AND I,0C
	LSR
	LSR
	ADC I,2
	STA NLIVES		;2 TO 5 LIVES TO START WITH
	LDA MODE
	BMI 7$			;NOT IN GAME PLAY
50$:	RTS

7$:	LDA TEMP2
	BEQ 8$			;NO FREE PLAY MESSAGE
	JSR MESS		;GAME COST MESSAGE
8$:	LDA FRAME
	AND I,20
	ASL
	ASL
	STA TEMP2
	LDA $$CRDT
	ORA $CNCT
	BEQ 85$			;NO CREDITS IN YET
	LDX TCMFLG
	BPL 10$			;NOT TWO CREDIT MINIMUM
	CMP I,2
	BCC 95$			;FLASH TCM WHEN CREDITS < 2
	LDA I,0
	STA TCMFLG		;CLEAR FLAG WHEN CREDITS > 1
	LDA I,8A		;ERASE "2 CREDIT MINIMUMS" MESSAGE
	BNE 97$			;ALWAYS

85$:	LDA OPTNS
	AND I,80
	STA TCMFLG		;SET/CLEAR 2 CREDIT MINIMUM FLAG
	EOR I,8A
	JSR MESS		;EITHER CLEAR OR WRITE MESSAGE
	LDX I,-1
	STX STRTL1		;TURN OFF START LED'S
	STX STRTL2
92$:	RTS


95$:	LDA I,10.
	ORA TEMP2
97$:	JSR MESS		;FLASH TCM
10$:	LDA I,9
	JSR MESS		;"CREDITS"
	LDA $$CRDT
	CMP I,10.
	BCC 15$			;9 OR LESS CREDITS
	LDA I,21
	JSR CHAR		;10 TO 16 CREDITS
	LDA $$CRDT
	SEC
	SBC I,10.
15$:	ORA I,20		;1 THRU 9 ARE 21 TO 29
	JSR CHAR
	LDA $CNCT
	BEQ 18$			;IF NO 1/2 CREDIT
	LDA I,1E
18$:	JSR CHAR		;PUT UP 1/2 OR BLANK
	LDX $$CRDT
	BEQ 92$			;IF NO CREDITS
	LDA TCMFLG
	BMI 92$			;NOT ENOUGH CREDIT IN TCM
	LDA TEMP2
	STA STRTL1		;FLSH 1 PLAYER START LED
	CPX I,02
	BCC 20$			;ONLY ENOUGH CREDIT FOR ONE
	STA STRTL2		;FLASH 2 PLAYER START LED
	LDX I,02
	LDA START2
	AND I,02
	BNE 20$			;IF 2 PLAYER GAME NOT STARTED
	LDA NLIVES
	STA LIVES+1
	DEC $$CRDT		;DELETE CREDIT
	BPL 25$			;ALWAYS.

20$:	LDA START1
	LDX ONE
	LSR
	BCS 92$			;IF 1 PLAYER GAME NOT STARTED
25$:	DEC $$CRDT
	LDA I,-1
	STA STRTL1
	STA STRTL2		;TURN OFF START LED'S
	LDA I,0
	STA GTIME
	STA GTIME+1		;CLEAR GAME TIMER
	STA SDBNCE		;ALLOW FOR DEBOUNCE IN "GETINT"
	STA $BC
	STA $BCCNT		;CLEAR BONUS ADDER FLAGS
	STX NPLAYR		;NUMBER OF PLAYERS
	STA X,STRTL1-1		;TURN ON APPROPRIATE START LED
	LDX NLIVES
	DEX
	STX LIVES		;NUMBER OF LIVES-1 (WE ARE PLAYING WITH ONE)
	INC MODE		;NOW IN PLAY MODE
	JSR COPYHS		;COPY HIGH SCORES TO EA ROM BUFFER
	JSR BONUS1		;GET BONUS LEVEL VALUES
	STA BONUSL
	STA BONUSL+1
	LDA Y,BONUSV+1		;SET INITIAL BONUS VALUES
	STA BONUSM
	STA BONUSM+1
	LDA COCKSW
	AND I,10
	BEQ 30$			;NOT COCKTAIL NOW
	LDA I,80
	STA CKIND		;COCKTAIL
	JSR CLEAR		;JUST IN CASE WE WERE ASKING FOR PLAYER 2'S INITIALS
30$:	JSR INIT		;INITIALIZE EVERYTHING
	JMP DLIVES		;DISPLAY LIVES

	.SBTTL	CLRCH-COLOR RAM INITIALIZATION
;CLRCH-COLOR RAM INITIALIZATION
;
;ENTRY (X)=INDEX FOR COLORS
CLRCH:	LDA X,99$
	PHA
	LDA X,99$+1
	TAY
	LDA X,99$+2
	TAX
	PLA
	STX COLORR+0A		;EYES
	STX COLORR+02		;OUTSIDE OF MUSHROOMS AND ALL ALPHANUMERICS
	STA COLORR+0B		;BODY=
	STA COLORR+01		;INSIDE OF MUSHROOMS AND GUN FOR NUMBER OF LIVES
	STY COLORR+09		;LEGS=
	STY COLORR+03		;INSIDE OF POISON MUSHROOM
	RTS

99$:	.BYTE 0D,0,0E,2,4,1,0E,1,0C,4,1,0B
	.BYTE 1,0C,0A,9,0B,4,0C,0D,0A,9,0C,0E
	.BYTE 0A,0E,1,0B,1,4,1,0,6,0D,0E,0A
	.BYTE 0E,0C,0B,0,0D,2



	.SBTTL	COPYHS-COPY HIGH CORES TO EA ROM BUFFER
COPYHS:	LDA I,-1
	STA UPDFLG
	STA UPDFLG+1		;STOP GETTING ANY INITIALS FROM PLAYER
	LDX I,8
10$:	LDA X,HSCORE
	STA X,EAROM		;SAVE FIRST 3 SCORES
	LDA X,INITL
	STA X,EAROM+9		;SAVE FIRST 3 PLAYERS INITIALS
	DEX
	BPL 10$
	JMP CKSUM		;CHECK EAROM

20$:	RTS


	.SBTTL	DLIVES-DISPLAY LIVES
;
DLIVES:	LDA I,6
	STA TEMP1		;MAX NUMBER OF LIVES
	LDA I,PLYFLD/100
	EOR CK03
	AND I,06		;EITHER STARTING AT 4XX OR 6XX
	STA TEMP4+1
	LDA I,0DF
	EOR CK3F
	STA TEMP4
	LDX LIVES
10$:	LDA I,1F		;PICTURE OF SHIP
	DEX
	BPL 15$
	LDA I,0			;NO MORE SHIPS
15$:	JSR CHAR		;PUT OUT SHIP OR BLANK
	DEC TEMP1
	BNE 10$
	LDA I,6
	EOR CK03
	STA TEMP4+1
	LDA I,5F
	EOR CK3F
	STA TEMP4
	LDA I,6
	STA TEMP1
	SEC
	SBC LIVES+1
	TAX			;NUMBER OF BLANKS TO DISLAY
20$:	LDA I,0
	DEX
	BPL 25$			;PUT OUT BLANK
	LDA I,1F
25$:	JSR CHAR		;DISPLAY SHIP OR BLANK.
	DEC TEMP1
	BNE 20$
	RTS



	.SBTTL	EXPLOD-EXPLODE CENTIPEDE SEGMENTS AND PLAYER
;
EXPLOD:	LDX I,13.
10$:	LDY X,MOBJP
	CPY I,0F9
	BCC 25$			;IF SEGMENT IS ALIVE
	CPY I,0FA
	BCC 20$			;IF FINISHED EXPLODING
	DEC X,MOBJP		;NEXT PICTURE
	BNE 25$			;ALWAYS

20$:	CPX I,13.
	BNE 25$			;IF NOT SPIDER
	LDA PLAYP
	AND I,0AF
	BNE 25$			;IF PLAYER IS DEAD
	LDA PTS
	EOR CKC0
	STA BUGP		;PUT UP POINTS FOR KILLING SPIDER
25$:	DEX
	BPL 10$
	LDA PLAYP
	AND I,0AF
	BEQ 30$			;IF PLAYER STILL ALIVE
	LDA FRAME
	AND I,03
	BNE 30$			;SLOW DOWN EXPLOSION
	LDA PLAYP
	CMP I,28
	BCS 30$			;PLAYER IS FINISHED EXPLODING
	INC PLAYP		;CHANGE EXPLOSION PICTURE
	CMP I,27
	BNE 30$			;IF NOT LAST FRAME OF EXPLOSION
	LDA I,0
	STA MEM
	LDA I,4
	STA MEM+1		;RESTORE MUSHROOMS
30$:	RTS

	.PAGE
	.SBTTL	GETINT-GET PLAYERS INITIALS FOR HIGH CORE
;GETINT-GET PLAYERS INITIALS FOR HIGH SCORE
;
;EXIT (CC)=POSITIVE FLAG SET IF WE ARE ENTERING INITIALS
GETINT:	LDA UPDFLG
	AND UPDFLG+1
	BPL 10$			;GET PLAYERS INITIALS
	RTS

10$:	LDA UPDFLG+1
	BMI 15$			;NOT PLAYER 2
	LDA CKIND
	BPL 15$			;NOT COCKTAIL NOW
	LDA CKC0
	BNE 15$			;WE'VE ALREADY BEEN HERE
	LDA I,82
	STA CKIND
	JSR CKSET
	JSR SWAP
	JSR UPSCRE		;PUT UP SCORES FOR PLAYER 2
	JSR INIT1		;PUT UP PLAYER FOR COCKTAIL
15$:	LDA NPLAYR
	LSR
	BEQ 25$			;IF LAST GAME WAS 1 PLAYER GAME
	LDA I,0
	JSR MESS		;DISPLAY "PLAYER"
	LDY I,02
	LDX UPDFLG+1
	BPL 20$			;IF PLAYER 2
	DEY			;LDY I,01
20$:	STY PLAYER
	TYA
	ORA I,20
	JSR CHAR		;DISPLAY PLAYER NUMBER
25$:	LDA I,8
	JSR MESS		;DISPLAY "GREAT SCORE"
	LDA I,5
	JSR MESS		;DISPLAY "ENTER YOUR INITIALS"
	LDA I,89
	EOR CKBF
	STA TEMP4		;SET POSITION
	LDA I,05
	EOR CK03
	STA TEMP4+1
	LDX PLAYER
	LDY X,UPDFLG-1
	STY TEMP2
	TYA
	CLC
	ADC UPDINT
	STA TEMP2+1		;INDEX FOR THE INITIAL WE ARE WORKING ON
	JSR INITAL		;DISPLAY INITIAL
	LDY TEMP2
	INY
	JSR INITAL		;DISPLAY INITIAL
	LDY TEMP2
	INY
	INY
	JSR INITAL		;DISPLAY THIRD INITIAL
50$:	LDA A,FIRE
	LDX CKC0
	BEQ 51$			;NOT COCKTAIL NOW
	LSR			;D3=FIRE FOR PLAYER 2 IN COCKTAIL
51$:	LSR			;D2=FIRE SWITCH
	LSR
	LSR
	ROL SDBNCE
	LDA SDBNCE
	AND I,1F
	CMP I,18
	BNE 60$			;2 FRAMES OFF 3 FRAMES ON
	INC UPDINT
	LDA UPDINT
	CMP I,03
	BCC 55$			;IF WE ARE NOT DONE
52$:	LDX PLAYER
	LDA I,-1
	STA X,UPDFLG-1		;CLEAR UPDATING FLAG
	LDA CKC0
	BEQ 53$			;IF NOT COCKTAIL
	LDA I,80
	STA CKIND		;SET FOR PLAYER 1 COCKTAIL
	JSR CLEAR		;CLEAR COCKTAIL INDICATORS
	JSR SWAP		;SWITCH TO PLAYER 1'S SCREEN
	JSR INIT1		;RESET PLAYERS GUN
	JSR UPSCRE		;PUT UP SCORES
	JSR CENTPC		;SET CENTIPEDE FOR PLAYER 1
	JSR BUGOFF		;SET BUG FOR PLAYER 1
	JSR ANTPC		;SET ANT FOR PLAYER 1
	LDA UPDFLG
	BMI 62$			;PLAYER 1 DIDN'T MAKE IT TO HIGH SCORE TABLE
53$:	LDA UPDFLG
	AND UPDFLG+1
	BMI 61$			;REMOVE MESSAGES
54$:	LDX I,0
	STX UPDINT		;START NEXT PLAYER
	RTS

55$:	INC TEMP2+1
	LDX TEMP2+1
	LDA I,0F4		;ABOUT 50 SECONDS
	STA FRAME+1		;RESET TIMEOUT
	LDA I,1
	STA X,INITL		;SET INITIAL TO A
60$:	LDA FRAME+1
	BEQ 52$			;TIME OUT - BACK TO ATTRACT
	BNE 65$

61$:	LDA I,88
	JSR MESS		;REMOVE "GREAT SCORE"
	LDA I,85
	JSR MESS		;REMOVE "ENTER INITIALS"
	LDA I,0
	STA 589			;REMOVE INITIALS
	STA 5A9
	STA 5C9
62$:	JSR COPYHS		;COPY HIGH SCORES TO EAROM BUFFER
	JSR SCORES		;DISPLAY HIGH SCORE TABLE
	LDX NPLAYR
	STX FRAME+1		;DO NOT REWRITE THE SCREEN FOR AWHILE
	DEX
	BEQ 54$			;IF ONLY 1 PLAYER GAME
	LDA I,80
	JSR MESS		;CLEAR "PLAYER" MESSAGE
	LDA I,0
	TAY
	STA NY,TEMP4		;CLEAR PLAYER NUMBER
	BEQ 54$			;ALWAYS

65$:	LDA FRAME
	AND I,07
	BNE 90$			;EVERY 8TH FRAME
	LDX I,-1
	LDA I,0
	LDY TB			;GET HORIZONTAL TRACKBALL READINGS
	STA TB			;CLEAR FOR NEXT FRAME
	BPL 70$			;IF GOING ONE WAY
	LDX I,1
	TYA
	JSR COMP		;COMPLEMENT A
	TAY
70$:	CPY I,04
	BCC 90$			;IF NO CHANGE
	TXA
	EOR CKFE
	LDX TEMP2+1
	CLC
	ADC X,INITL		;CHANGE INITIAL
	BMI 78$			;BEFORE A BLANK MUST BE Z
	CMP I,27.
	BCC 85$			;IF LESS THAN Z+1
	LDA I,0			;BEFORE A COMES A BLANK
	BEQ 85$

78$:	LDA I,26.		;MAKE IT A Z
85$:	STA X,INITL
90$:	LDA I,0			;MUST BE POSITIVE ON RETURN
	RTS


	.PAGE
	.SBTTL	INIT-INITIALIZE EVERYTHING
;INIT-INITIALIZE EVERYTHING
;
INIT:	LDA I,20
	STA AUDCTL
	LDA I,12.
	STA CENTIN
	STA CENTIN+1		;SET CENTIPEDE SIZE
	LDA ONE
	STA PLAYER		;SET PLAYER NUMBER
	STA PLAYDH		;DIRECTION FOR ATTRACT
	STA PLAYDV
	LDA I,02		;FAST TO START WITH
	STA CENTIS
	STA CENTIS+1		;SET CENTIPEDE SPEED
	LDX I,NCHAN
	LDA I,0
	STA SKCTL		;INITIALIZE POKEY
5$:	STA X,CHAN0		;INITIALIZE SOUNDS
	DEX
	BPL 5$
	LDX I,05
6$:	STA X,SCORE0
	DEX
	BPL 6$			;CLEAR ALL SCORES
	LDA RNGEN
	EOR RNGEN		;BE SURE ITS A POKEY
	CLC
	ADC $$CRDT
	STA $$CRDT		;ADD CREDITS IF ITS NOT A POKEY
	LDA I,3
	STA SKCTL		;TURN ON POKEY
	JSR CENTPC		;INITIALIZE CENTIPEDE
	LDA I,0C0
	STA COUNT1		;INITIALIZE COUNTERS FOR ADDING NEW HEADS
	STA COUNT3
	STA COUNT3+1
	JSR BUGOFF		;INITIALIZE BUG
	JSR ANTPC		;ANT
INITSC =.
	LDA I,0F
	STA COLORR		;BLACK BACKGROUND
	LDX PLAYER
	LDA I,0
	STA X,LCOLOR-1		;MAKE SURE OF CORRECT STARTING COLOR
	TAX			;CALL
				;START COLOR INDEX AT BEGINNING
	JSR CLRCH		;SET UP COLOR RAM
	LDX I,0
	TXA
10$:	STA AX,PLYFLD		;CLEAR PLAYFIELD
	STA AX,PLYFLD+100
	STA AX,PLYFLD+200
	STA AX,PLYFLD+300
	INX
	BNE 10$
	LDX PLAYER
	STA X,MUSH-1		;CLEAR COUNT AFTER ATTRACT
	LDX I,1B
	STX TEMP1
	LDX I,45.
20$:	LDA RNGEN		;RANDOM NUMBER
	AND I,0E0
	ORA TEMP1
	STA TEMP2
	LDA RNGEN		;RANDOM NUMBER
	AND I,03
	ORA I,04
	STA TEMP2+1
	STX TEMP3
	LDY I,0
	LDA TEMP2
	AND I,1F
	LDX CKC0
	BEQ 25$			;NOT TIME FOR COCKTAIL
	CMP I,14
	BCC 30$			;IF ON UPPER PART OF SCREEN FOR COCKTAIL
	BCS 27$			;ALWAYS

25$:	CMP I,0C
	BCS 30$			;IF ON UPPER PART OF SCREEN
27$:	LDA NY,TEMP2
	BNE 30$			;DON'T INC IF MUSHROOM ALREADY THERE
	LDX PLAYER
	INC X,MUSH-1		;ADD ONE TO MUSHROOM COUNT
30$:	LDA I,3F
	EOR CKC0
	STA NY,TEMP2		;PUT UP OBSTACLE
	LDA TEMP1
	SEC
	SBC I,01
	CMP I,2
	BCS 35$			;STAY OFF BOTTOM ROW
	LDA I,1B
35$:	STA TEMP1
	LDX TEMP3
	DEX
	BPL 20$			;PUT UP OBSTACLES
INIT1:	LDA I,10
	EOR CK40
	STA PLAYP		;GUN
	LDA I,80
	STA PLAYH		;GUN HPOS
	STA SHOTH
	LDA I,8
	EOR CKF8
	STA PLAYV
	LDA I,0C
	EOR CKE0
	STA SHOTV		;SHOT VPOS
	LDA I,11
	EOR CK40
	STA SHOTP
	RTS
.SBTTL MOTION-MOTION UPDATE
;MOTION-UPDATE POSITION OF MOTION OBJECTS
;
MOTION:	LDA DELAY
	BEQ 2$			;IF NO DELAY
	RTS

2$:	LDX I,NCENT-1
	LDA FRAME	
	AND I,0F
	BNE 5$			;IF NOT 1/4 SECOND BOUNDARY
	LDA I,07
	STA CHAN1		;START LITTLE FEET NOISE
5$:	LDA X,MOBJP
	BPL 6$			
	JMP 30$			;IF EXPLOSION OR VACANT POSITION

6$:	LDA FRAME
	AND I,01
	BNE 7$			;SLOW DOWN LEGS
	LDA X,MOBJP
	CLC
	ADC I,01
	AND I,0F7
	STA X,MOBJP		;MOVE LEGS
7$:	LDY I,1
	LDA X,MOBJV
	EOR CKF8
	CMP I,9
	BCS 71$			;IF NOT ON BOTTOM ROW
	LDA X,MOBJP
	CMP I,10
	BCS 70$			;IF BODY OR POISONED HEAD
	STY NEWD		;START ADDING NEW HEADS
70$:	LDA X,MOBJV		;RESTORE A
71$:	AND I,07
	BNE 15$			;DON'T SPEED UP IF TURNING
	TYA
	LDY PLAYER
	CMP Y,DEAD-1
	BNE 78$			;IF MORE THAN 1 LIVE SEGMENT
	LDA I,2
	LDY X,MOBJDH
	BPL 72$
	LDA I,-2
72$:	STA X,MOBJDH		;SPEED UP LAST HEAD
	LDA I,2
	LDY X,MOBJDV
	BPL 73$
	LDA I,-2
73$:	STA X,MOBJDV		;SPEED UP LAST HEAD
78$:	LDA X,MOBJP
	AND I,40
	BEQ 10$			;HEAD
	LDA X,MOBJV-1
	SEC
	SBC X,MOBJV		;/(PREV.SEG.VPOS)-(CUR.SEG.VPOS)/
	JSR ABS
	CMP I,08
	BCS 15$			;DIFFERENCE=8
8$:	JMP 20$
	
10$:	LDA X,MOBJP
	AND I,20
	BNE 15$			;IF HEAD IS POISONED GO TO BOTTOM OF SCREEN
	LDA X,MOBJH
	CMP I,0F0
	BCC 11$			;IF NOT AT LEFT EDGE
	LDY X,MOBJDV
	BEQ 115$
	LDY X,MOBJDH
	BPL 15$			;CHANGE VPOS
	BMI 12$			;DO NOT CHANGE VPOS
	
11$:	CMP I,10
	BCS 12$			;IF NOT A RIGHT EDGE
	LDY X,MOBJDV
	BNE 117$
115$:	JMP 25$

117$:	LDY X,MOBJDH
	BMI 15$			; CHANGE VPOS
12$:	JSR CALLS
	JSR OBSTAC
	BEQ 13$			;NO OBSTACLE
	CMP I,38
	BCC 15$			;IF NOT A MUSHROOM
	CMP I,3C
	BCS 15$			;IF NOT A POISIONED MUSHROOM
	LDA X,MOBJP
	ORA I,20
	STA X,MOBJP		;SET HEAD AS POISONED
	BCC 15$			;ALWAYS

13$:	JSR OVRLAP		;CHECK FOR OVERLAP
	BCC 8$			;NO OVERLAP OF SEGMENTS
15$:	LDA X,MOBJV
	EOR CKF8
	LDY X,MOBJDV
	BEQ 115$		;IF DV=0 TURN AROUND
	BPL 16$			;GOING DOWN SCREEN
	LDY CKC0
	BEQ 155$		;NOT COCKTAIL NOW
	EOR CKF8
	CMP I,0C9
	BCC 167$		;TURN DOWN IN COCKTAIL
	BCS 18$			;ALWAYS

155$:	CMP I,30
	BCS 167$			;TURN DOWN
	BCC 18$			;ALWAYS

16$:	CMP I,9
	BCS 18$			;IF NOT AT BOTTOM
	LDA X,MOBJP
	AND I,40
	BNE 167$		;IF NOT A HEAD
	LDA X,MOBJP
	AND I,0DF
	STA X,MOBJP		;CLEAR POISONED BIT
	CPX I,NCENT-1
	BEQ 167$		;LAST CENTIPEDE ENTRY
	TXA
	TAY
	INY
	LDA Y,MOBJP
	BMI 167$		;IF DEAD
	AND I,40
	BEQ 167$		;NO BODY TO THIS CENTIPEDE
163$:	CPY I,NCENT-1
	BEQ 164$		;NO MORE ENTRIES
	LDA Y,MOBJP+1
	BMI 164$		;IF NEXT SEGMENT DEAD
	AND I,40
	BNE 165$		;IF NEXT SEGMENT HEAD
164$:	LDA Y,MOBJV
	EOR CKF8
	CMP I,9
	BCS 167$		;KEEP CENTIPEDE WHOLE IF TAIL IS NOT ON THE BOTTOM ROW
	LDA Y,MOBJP
	AND I,07
	STA Y,MOBJP		;CHANGE TO HEAD
	LDA Y,MOBJDH
	JSR COMP
	STA Y,MOBJDH		;REVERSE HDIR
	LDA Y,MOBJV
	AND I,0F8
	STA Y,MOBJV		;JUST IN CASE HE IS TURNING
	LDA I,0			;DV=0 FOR ONE LINE
	STA Y,MOBJDV
	BEQ 167$		;ALWAYS

165$:	INY
	CPY I,NCENT
	BCC 163$		;GO THRU AL OBJECTS
167$:	LDA X,MOBJDV
	JSR COMP
	STA X,MOBJDV		;COMPLEMENT MOBJDV
18$:	LDA X,MOBJV
	LDY CKC0
	BEQ 185$		;NOT COCKTAIL NOW
	CLC
	ADC X,MOBJDV
	JMP 19$			;ALWAYS

185$:	SEC
	SBC X,MOBJDV
19$:	STA X,MOBJV		;CHANGE VPOS
20$:	LDA X,MOBJDH
	CLC
	ADC X,MOBJH
	STA X,MOBJH		;CHANGE HPOS
	JSR PLAY		;SEGMENT AND PLAYER COLLISION?
	BCC 35$			;IF PLAYER IS DEAD
	LDA X,MOBJV
	AND I,07
	CMP I,04
	BNE 30$			;NOT TIME TO CHANGE DIRECTION
25$:	LDA X,MOBJDH
	JSR COMP
	STA X,MOBJDH		;CHANGE DIRECTION
	LDY X,MOBJDV
	BNE 30$			;IF VDIR IS NOT ZERO
	ORA I,0
	BMI 27$			;USE NEGATIVE HDIR=VDIR
	JSR COMP
27$:	STA X,MOBJDV		;SET VDIR=HDIR
	LDA I,4
	LDY X,MOBJDH
	BPL 28$			;GO IN CORRECT DIRECTION AWAY FROM DGE
	LDA I,-4
28$:	CLC
	ADC X,MOBJH
	STA X,MOBJH		;GET AWAY FROM EDGE
30$:	DEX
	BMI 35$
	JMP 5$

35$:	RTS
	.PAGE
.SBTTL MOVE-MOVE PLAYER
;MOVE-MAKE TRACKBALL CALCULATIONS AND MOVE PLAYER
;
MOVE:	LDA MODE
	BPL 2$			;IF NOT ATTRACT
1$:	RTS

2$:	LDA PLAYP
	AND I,0AF
	BNE 1$			;HE'S DEAD JIM, DON'T MOVE HIM (THE PLAYER)
202$:	LDA PLAYV
	STA TEMP2		;SAVE OLD VERTICAL POSITION
	LDY MCKSUM		;IT BETTER BE ZERO
	LDA TB			;HORIZONTAL CHANGE IN TRACKBALL
	STY TB			;CLEAR FOR NEXT FRAME
	JSR TBLMT
	ADC PLAYHL
	STA PLAYHL		;KEEP FINER RESOLUTION
	TYA
MHOR	=.
205$:	ADC PLAYH
	TAX
	STA TEMP1		;OBSTAC CALLS
	LDY I,0
	LDA PLAYV
	JSR OBSTAC
	BNE 5$			;OBSTACLE ENCOUNTERED
	TXA
	CMP I,0F4
	BCC 3$			;IF NOT AT LEFT EDGE
	LDA I,0F4		;KEEP AT EDGE
	BNE 10$			;ALWAYS

3$:	CMP I,0B
	BCS 10$			;IF NOT AT RIGHT EDGE
	LDA I,0B		;KEEP AT EDGE
	BNE 10$			;ALWAYS

5$:	LDA PLAYH
10$:	STA PLAYH		;UPDATE HPOS
	LDY MODE
	BPL 20$			;IF NOT IN ATTRACT
	RTS
	
20$:	LDY I,0
	LDA TB+2
	STY TB+2		;CLEAR FOR NEXT FRAME
	JSR COMP		;DIRECTIONS ARE REVERSED RELATIVE TO MOTION OBJECT VIDEO
	JSR TBLMT
	ADC PLAYVL		;KEEP LSB FOR VERTICAL DIRECTION
	STA PLAYVL
	TYA
MVER	=.
35$:	ADC PLAYV
	TAX
	LDY PLAYH
	STY TEMP1
	LDY I,0
	JSR OBSTAC
	BNE 45$			;OBSTACLE ENCOUNTERED
	TXA
	CMP I,8
	BCC 43$			;BELOW BOTTOM BOUNDARY
	CMP I,0F1
	BCS 40$			;BELOW BOTTOM BOUNDARY FOR COCKTAIL
	CMP I,80
	BCC 37$			;NOT COCKTAIL NOW
	CMP I,0C8
	BCS 50$			;WITHIN TOP BOUNDARY
	LDA I,0C8
	BNE 50$			;ALWAYS

37$:	CMP I,31
	BCC 50$			;WITHIN TOP BOUNDARY
	LDA I,30
	BNE 50$			;ALWAYS

40$:	LDA I,0F0
	BNE 50$			;ALWAYS

43$:	LDA I,8			;KEEP AT BOTTOM DGE
	BNE 50$			;ALWAYS

45$:	LDA PLAYV
50$:	STA PLAYV
	LDY MODE
	BPL 60$			;IF NOT IN ATTRACT
	RTS
	
RSHOT1	=.
60$:	LDA SHOTV
	LDX CKC0
	BEQ 65$			;NOT COCKTAIL NOW
	SEC
	SBC TEMP2
	BCS RSHOT		;TIME TO REPOSITION SHOT
	BCC 67$			;ALWAYS

65$:	SEC
	SBC TEMP2
	BCC RSHOT
67$:	JSR ABS
	CMP I,05
	BCS 70$			;SHOT IS GOING UP
RSHOT	=.
	LDA I,04
	EOR CKF8
	CLC
	ADC PLAYV
	STA SHOTV
	LDA PLAYH
	STA SHOTH		;REPOSTION SHOT ON GUN
70$:	LDA PLAYP
	AND I,0AF
	BEQ 80$			;IF PLAYER IS ALIVE
	LDA I,28
	STA SHOTP		;CLEAR SHOT PICTURE
80$:	RTS


;MUSH-CORRECT COUNT OF MUSHROOMS
;	ON BOTTOM OF SCREEN
;
;
MUSHDC:	LDA OBST
	AND I,1F
	LDX CKC0
	BEQ 5$			;NOT TIME FOR COCKTAIL
	CMP I,14
	BCC 10$			;IF ON UPPER HALF OF SCREEN FOR COCKTAIL
	BCS 7$			;ALWAYS

5$:	CMP I,0C
	BCS 10$			;IF MUSHROOM IS ON UPPER HALF OF SCREEN
7$:	LDX PLAYER		;INDEX
	DEC X,MUSH-1		;CORRECT MUSHROOM COURT
10$:	RTS


MUSHER:	LDY I,0
	LDA NY,OBST
	BNE 20$			;DON'T INCREMENT IF MUSHROOM ALREADY ON SCREEN
	LDA OBST
	AND I,1F
	BEQ 20$			;DON'T PUT ON SCREEN IF TOP ROW OF COCKTAIL
	CMP I,1F
	BEQ 20$			;DON'T PUT UP MUSHROOM IF ON TOP ROW
	LDX CKC0
	BEQ 5$			;NOT TIME FOR COCKTAIL
	CMP I,1E
	BEQ 20$			;NOT ON THE SAME ROW AS THE PLAYER
	CMP I,14
	BCC 10$			;IF ON UPPER PART OF SCREEN FOR COCKTAIL
	BCS 7$			;ALWAYS

5$:	CMP I,01
	BEQ 20$			;NOT ON THE SAME ROW AS THE PLAYER
	CMP I,0C
	BCS 10$			;IF ON UPPER PART OF SCREEN
7$:	LDX PLAYER
	INC X,MUSH-1		;ADD TO MUSHROOM COUNT
10$:	LDA I,3F
	EOR CKC0
	STA NY,OBST		;PUT MUSHROOM ON SCREEN
20$:	RTS
	.PAGE
	.SBTTL	NEWHD-PUT IN NEW HEAD
;NEWHD-PUT IN NEW HEAD AFTER DEAD=1
;
;
NEWHD:	LDA NEWD
	BEQ 40$			;NOT TIME FOR NEW HEADS
	LDA DELAY
	BNE 40$			;NOT WHEN PLAYER IS DEAD
	LDA COUNT1
	BEQ 10$
	DEC COUNT1
	RTS

10$:	LDX PLAYER
	LDY I,NCENT-1
12$:	LDA Y,MOBJP
	BMI 20$			;IF SEGMENT IS DEAD
	DEY
	BPL 12$			;CONTINUE LOOKING FOR DEAD SEGMENT
	RTS

20$:	LDA I,0
	STA Y,MOBJP		;MAKE HEAD PICTURE
	LDA I,40
	EOR CKF8
	STA Y,MOBJV		;VPOS FOR NEW HEAD
	LDA I,0FC
	STA Y,MOBJH		;HPOS FOR NEW HEAD
	LDA I,2
	STA Y,MOBJDV		;NEW BUG GOES FAST
	LDA X,COUNT3-1
	CMP I,60		;1 1/2 SECONDS
	BCC 25$			;SMALLEST VALUE WE ALLOW
	SBC I,8
	STA X,COUNT3-1		;DECREMENT
25$:	STA COUNT1
	LDA RNGEN
	AND I,02
	BNE 30$
	LDA I,4
	STA Y,MOBJH
	LDA I,-2
30$:	STA Y,MOBJDH
	INC X,DEAD-1		;DEAD=DEAD+1
40$:	RTS
	.PAGE
	.SBTTL OBSTAC-OBSTACLE
;OBSTAC-CALCULATE OBSTACLE POSITION
;
;ENTRY	(A)=MOBJV
;	(Y)=MOBJDH
;	(TEMP1)=MOBJH
;EXIT	(Z)=0=OBSTACLE
;	   =1=NO OBSTACLE
;	(Y)=0
;	(A)=PLAYFIELD STAMP CODE
;	(OBST,OBST+1)=ADDRESS OF PLAYFIELD STAMP
;
OBSTAC:	LSR
	LSR
	LSR
	ADC I,0			;ADD ONE IF HALF WAY TO NEXT ROW
	STA OBST		;LSB OF SCREEN POSITION
	LDA I,1			;BASE ADDRESS OF PLAYFIELD/4
	STA OBST+1		;MSB OF SCREEN POSITION
	TYA
	ASL
	ASL
	ASL			;8*DIRECTION
	CLC
	ADC TEMP1		;LOOK FOR OBSTACLE AT
	STA TEMP1		;(H',V')=(H+8*D,V')
	LDA I,0F7
	SEC
	SBC TEMP1
	BCS 5$			;DO NOT WRAP AROUND LEFT EDGE OF SCREEN
	LDA I,0			;USE LEFT MARGIN
5$:	AND I,0F8
	ASL
	ROL OBST+1
	ASL
	ROL OBST+1
	ORA OBST		;COMPLETE LSB
	LDY OBST+1
	CPY I,07
	BNE 10$			;NOT OFF RIGHT SIDE
	CMP I,0C0
	BCC 10$			;WE ARE ON PLAYFIELD
	AND I,1F		;STAY ON RIGHT SIDE OF PLAYFIELD
	ORA I,0A0
10$:	STA OBST		;PLAYFIELD ADDRESS=
				;800+V/8+((F7-H')^F8)*4
	LDY I,0
	LDA NY,OBST		;OBSTACLE?
	BEQ 20$
	EOR CKC0		;LOOKS LIKE NOT COCKTAIL
20$:	RTS
	.PAGE
	.SBTTL OVRLAP-CHECK FOR OVERLAP
;OVRLAP-CHECK FOR OVERLAPPING SEGMENTS
;
;ENTRY	(X)=INDEX OF HEAD
;EXIT	(CC)=CARRY SET IF OVERLAP OCCURS
OVRLAP:	STX TEMP1
	STX TEMP1+1
	INC TEMP1+1
	LDY I,12.
10$:	LDA Y,MOBJV
	CMP X,MOBJV
	BNE 30$			;NOT ON SAME LINE
	LDA Y,MOBJP
	CMP I,0F4
	BCS 30$			;IF DEAD SEGMENT
	CPY TEMP1
	BEQ 30$			;DO NOT COUNT US
	LDA X,MOBJH
	SEC
	SBC Y,MOBJH
	EOR X,MOBJDH		;LOOK ONLY IN FROM OF US
	CMP I,0F4
	BCS 40$			;WE HAVE AN OVERLAP
30$:	DEY
	BPL 10$
	CLC
40$:	RTS

	.SBTTL PLAY-CHECK FOR PLAYER COLLISION
;PLAY-IF CENTIPEDE COLLIDES WITH PLAYER, DESTROY PLAYER
;ENTRY	(X)=INDEX OF CURRENT OBJECT
;EXIT	(X)=INDEX OF CURRENT OBJECT
;	(CC)=CARRY CLEAR IF PLAYER HAS HAD A COLLISION
;
PLAY:	LDA X,MOBJH
	SEC
	SBC PLAYH		;X=/(OBJECT HPOS)-(PLAYER HPOS)/
	JSR ABS			;ABSOLUTE VALUE
	CPX I,13.
	BNE 10$			;IF NOT BUG
	CMP I,10.
	BCC 15$			;IF COLLISION POSSIBLE
25$:	RTS

10$:	CMP I,07
	BCS 25$			;DIFF > 6
15$:	STA TEMP2
	LDA X,MOBJV
	SEC
	SBC PLAYV		;Y=/(OBJECT VPOS)-(PLAYER VPOS)/
	JSR ABS
	CMP I,7
	BCS 25$			;DIFF>6
	CLC
	ADC TEMP2		;X+Y
	CPX I,13.
	BEQ 40$			;IF BUG
	CMP I,0C
30$:	BCS 25$			;IF NOT A HIT
	LDA I,30
	STA DELAY		;SET DELAY BEFORE NEXT SETUP
	LDA I,20
	STA PLAYP		;CHANGE PICTURE
	LDA I,0FF
	STA X,MOBJP
	LDA I,28
	STA SHOTP		;BLANK SHOT
	LDA MODE
	BMI 35$			;IF IN ATTRACT
	LDA I,13
	STA CHAN5		;EXPLOSION SOUND FOR PLAYER
35$:	LDA I,0			;NO OTHER SOUNDS
	STA CHAN0
	STA CHAN1
	STA CHAN2
	STA CHAN3
	STA CHAN6
	CLC
	RTS			;CHANGE PICTURE TO EXPLOSION

40$:	CMP I,14.		;CHECK DIAGONAL TOO
	JMP 30$


	.SBTTL	RESTOR-RESTORE MUSHROOMS
;
;ENTRY (MEM,MEM+1)=ADDRESS OF CURRENT PLAY FIELD VALUE
RESTOR:	LDY I,0
	LDA FRAME
	AND I,07
	BNE 5$			;EVERY 16 FRAMES
	LDA CHAN5
	BNE 5$			;WAIT FOR PLAYER EXPLOSION TO FINISH
1$:	LDA MEM+1
	BEQ 5$			;ALL DONE
	CMP I,07
	BNE 10$			;NOT DONE YET
	LDA MEM
	CMP I,0C0
	BCC 10$			;NOT DONE YET
	STY MEM+1		;CLEAR POINTER
5$:	RTS

10$:	LDA NY,MEM		;LOAD PLAYFIELD
	AND I,3F
	CMP I,38
	BCC 20$			;IF NOT A MUSHROOM
	CMP I,3F
	BCS 20$			;IF FULL MUSHROOM
	LDA I,3F
	EOR CKC0
	STA NY,MEM		;RESTORE TO FULL MUSHROOM
	LDA I,0
	STA TEMP1
	LDA I,5
	JSR SCORN1		;AWARD 5 PTS. FOR RESTORED MUSHROOMS
	LDA I,0FF
	STA MOBJP+NCENT-1	;PUT MOTION OBJECT EXPLOSION ON MUSHROOM
	LDA MEM+1
	STA TEMP1
	LDA MEM
	ASL
	ROL TEMP1
	ASL
	ROL TEMP1
	ASL
	ROL TEMP1
	STA MOBJV+NCENT-1		;EXPLOSION VERTICAL
	LDA TEMP1		;20 TO 3F
	AND I,1F		;REMOVE 400 BASE ADDRESS FOR PLAYFIELD
	EOR I,01F		;REVERSE SO LOWER LEFT CORNER IS F8
	ASL
	ASL
	ASL
	SBC I,3			;SUBTRACT 4-REMEMBER EXPLOSION IS CENTERED IN PICTURE
	STA MOBJH+NCENT-1
	INC MEM
	BNE 25$			;NO OVERFLOW
	INC MEM+1
25$:	LDA I,13
	STA CHAN0
	RTS

20$:	INC MEM
	BNE 1$			;NO OVERLFLOW
	INC MEM+1
	BNE 10$			;ALWAYS-BYPASS END OF PLAYFIELD CHECK


;SCORES-DISLAY HIGH SCORES

SCORES:	LDA I,7
	JSR MESS		;"HIGH SCORES"
	LDY I,0
	LDX I,5C		;FOR PLAYER 1'S VIEWPOINT ONLY
10$:	STX TEMP4
	LDA I,05
	STA TEMP4+1		;STARTING ADDRESS FOR THIS LINE
	LDA Y,HSCORE+2
	STY TEMP2		;SAVE INDEX INTO "HSCORE"
	SEC			;ZERO SUPPRESSION
	JSR DIGIT2
	LDY TEMP2
	LDA Y,HSCORE+1
	JSR DIGIT2		;2 MORE DIGITS
	LDY TEMP2
	LDA Y,HSCORE
	CLC			;NO ZERO SUPPRESSION
	JSR DIGIT2		;DISPLAY DIGITS
	LDA I,0
	JSR CHAR		;PUT OUT A BLANK
	LDY TEMP2
	JSR INITAL		;PUT UP INITIAL
	INC TEMP2
	LDY TEMP2
	JSR INITAL		;PUT UP 2ND INITIAL
	INC TEMP2
	LDY TEMP2
	JSR INITAL		;PUT UP 3RD INITIAL
	LDA TEMP4
	AND I,1F
	ORA I,40		;540+LINE NUMBER.
	TAX
	DEX			;NEXT LINE DOWN FOR SCORES
	LDY TEMP2
	INY
	CPY I,3*NSCORE
	BCC 10$			;IF MORE TABLES
	RTS

CKSUM1:	.BYTE 7C		;CHECKSUM 2800-2FFF
	.PAGE
	.SBTTL	SCORNG-SCORING
;SCORNG-AWARD SCORE
;
;ENTRY (A)=POINTS TO BE ADDED TO SCORE (1ST DIGIT)
;	(TEMP1)=POINTS TO BE ADDED TO SCORE+1 (2ND DIGIT)
;	(X)=INDEX OF CURRENT OBJECT
;
;EXIT	(X)=INDEX OF CURRENT OBJECT
;
SCORNG:	STX TEMP2
	LDX PLAYER
	DEC X,DEAD-1
	LDX TEMP2
SCORN1  =.
	LDY MODE
	BMI 30$			;IF IN ATTRACT
	STX TEMP2		;SAVE X
	SED
	LDX PLAYER
	CLC
	ADC X,SCORE0-1
	STA X,SCORE0-1		;INCREMENT 1ST DIGIT OF SCORE
	LDA X,SCORE1-1
	ADC TEMP1
	STA X,SCORE1-1
	BCC 15$			;IF NOT ON 10K BOUNDARY
	LDA X,COUNT3-1		;YES I KNOW I'M IN DECIMAL MODE
	SBC I,2
	STA X,COUNT3-1		;INCREASE FREQUENCY OF NEW HEADS
	LDA X,SCORE2-1
	CLC
	ADC I,1
	STA X,SCORE2-1
15$:	CLD
	LDX PLAYER
	LDA X,SCORE1-1
	CMP X,BONUSL-1
	LDA X,SCORE2-1
	SBC X,BONUSM-1
	BCC 25$			;NOT AT BONUS LEVEL
	JSR BONUS1		;GET BONUS LEVEL
	SED
	CLC
	ADC X,BONUSL-1
	STA X,BONUSL-1
	LDA Y,BONUSV+1		;SET NEXT BONUS LEVEL FOR THIS PLAYER
	ADC X,BONUSM-1
	STA X,BONUSM-1
	CLD
	LDA X,LIVES-1
	CMP I,6
	BEQ 25$			;NO MORE LIVES 6 IS MAX
10$:	BCS 10$			;OOPS-RESET
	INC X,LIVES-1		;ADD A LIFE AS A BONUS
	LDA I,17.
	STA CHAN4		;BONUS LIFE SOUND
	JSR DLIVES		;SHOW EXTRA LIFE
25$:	LDX TEMP2		;RESTORE X
30$:	RTS
	.PAGE
	.SBTTL	SCORP-MOVE AND START SCORPION
SCORP:	LDA ANTP
	EOR CKC0
	CMP I,34
	BCC 2$
	JMP 55$			;IF EXPLODING

2$:	CMP I,30
	BCS 50$			;THIS IS A SCORPION MOVE HIM
	LDA ANTV
	EOR CKF8
	CMP I,0F8
	BCC 3$			;AN ANT MUST BE MOVING NOW
	LDA FRAME
	BEQ 4$			;SCORP NOW
3$:	JMP 90$			;ALWAYS

4$:	LDX PLAYER
	LDA X,CENTIN-1
	CMP I,11.
	BCS 3$			;WAIT UNTIL WAVES WHERE THE CENTIPEDE IS SMALL
	LDA RNGEN
	AND I,03
	BNE 3$			;RANDOMLY ENTER SCORPION
	LDA I,20.
	STA CHAN6		;TURN ON SOUND
	LDA I,30
	EOR CKC0
	STA ANTP		;SET SCORPION PICTURE
	LDA X,SCORE2-1
	CMP I,2
	BCC 5$			;IF SCORE <20K, USE SLOW SPEED
	LDA RNGEN
	AND I,03
	BEQ 5$			;RANDOMLY USE SLOW SCORPION OVER 20K
	LDA I,2
	BIT RNGEN
	BPL 10$			;CHOOSE RANDOM DIRECTION
	LDA I,-2
	BNE 10$			;ALWAYS

5$:	LDA I,1
	BIT RNGEN
	BPL 10$			;CHOOSE RANDOM DIRECTION
	LDA I,-1
10$:	STA ANTDH
	LDA I,0
	STA ANTH		;START AT EDGE
	STA ANTDV		;CLEAR VERTICAL DIRECTION
	LDA RNGEN
	AND I,78
	CLC
	ADC I,70
	EOR CKF8
	STA ANTV		;SET VERTICAL POSITION IN UPPER HALF OF SCREEN
50$:	LDA PLAYP
	AND I,0AF
	BNE 58$			;IF PLAYER IS DEAD OR DYING
	LDA ANTH
	LDY CKC0
	BEQ 52$			;NOT COCKTAIL NOW
	SEC
	SBC ANTDH
	JMP 53$			;ALWAYS

52$:	CLC
	ADC ANTDH
53$:	STA ANTH		;NEW HORIZONTAL POSITION
	STA TEMP1		;FOR "OBSTAC" BELOW
	BNE 60$			;IF NOT OFF SCREEN
	BEQ 58$			;ALWAYS

55$:	EOR CKC0
	CMP I,0FA
	BCS 90$			;KEEP EXPLOSION GOING
58$:	JMP ANTPC		;RESET ENTRY TO ALLOW ANTS

60$:	LDA FRAME
	AND I,03
	BNE 70$			;EVERY FEW FRAMES CHANGE PICTURE
	LDA ANTP
	CLC
	ADC I,01		;UPDATE PICTURE NUMBER
	AND I,03
	ORA I,30		;SET CODE FOR 30 TO 33
	EOR CKC0
	STA ANTP
70$:	LDY I,0			;MAKE MUSHROOMS CHANGE WHEN SCORPION IS ON TOP
	LDA ANTV
	JSR OBSTAC		;CHECK FOR OBSTACLE
	CMP I,40
	BCS 90$			;IF NOT A MUSHROOM
	CMP I,3C
	BCC 90$			;NOT A MUSHROOM
	AND I,0FB
	EOR CKC0
	STA NY,OBST		;POISON THE MUSHROOM
90$:	RTS
	.PAGE
	.SBTTL CHECK FIRE SWITCH
;SHOOT-CHECK FIRE SWITCH AND FIRE SHOT
;
SHOOT:	LDA SHOTV
	LDY CKC0
	BEQ 2$			;NOT COCKTAIL NOW
	CLC
	ADC I,07
	EOR I,0FF
2$:	CMP I,0F1
	BCS 108$		;IF OFF TOP OF SCREEN-RESET POSITION
	LDA I,04
	EOR CKF8
	CLC
	ADC PLAYV		;SHOT MOVES UP 6 AND SHIP DOWN 3 MAX
	CMP SHOTV
	BNE 102$			;SHOT IS GOING UP
	LDA PLAYP
	AND I,0AF
	BNE 8$			;IF PLAYER IS NOT ALIVE
	LDA FIRE		;D2=FIRE SWITCH
	LDX MODE
	BPL 3$			;GAME PLAY MODE
	LDA RNGEN
3$:	CPY I,0C0
	BNE 5$			;NOT COCKTAIL NOW
	AND I,08		;D3=FIRE FOR COCKTAIL
	BEQ 10$			;SWITCH JUST PUSHED
	BNE 8$			;ALWAYS

5$:	AND I,04
	BEQ 10$			;SWITCH JUST PUSHED DOWN
8$:	JMP 30$			;FIRE SW OFF

10$:	LDA I,0B
	STA CHAN2		;SHOT SOUND
102$:	LDA SHOTH
	STA TEMP1		;WILL BE USED TO CHECK OBSTACLES
	LDA I,7
	EOR CKFE
	LDY SHOTV
	CLC
	ADC SHOTV
	STA SHOTV		;ADD 7 TO SHOT'S POSITION
	STY TEMP2
	LDA I,01
	EOR CKFF		;1 FOR PLAYER 1 AND -2 FOR PLAYER 2 COCKTAIL
	CLC
	ADC TEMP2
	LDY I,0			;GIVE NO DIRECTION "OBSTAC"
	JSR OBSTAC
	BEQ 11$			;NO OBSTACLE ENCOUNTERED
105$:	AND I,3F
	CMP I,38
	BCC 108$		;IF NOT A MUSHROOM
	SBC I,1			;CARRY IS SET
	CMP I,3B
	BEQ 106$		;END OF MUSHROOM
	CMP I,37
	BNE 107$		;NOT END OF MUSHROOM
106$:	LDA I,0
	STA TEMP1
	LDA I,01
	JSR SCORN1		;ADD 1 POINT FOR MUSHROOM
	LDY I,0
	LDA CKC0		;LEAVE IT ZERO IN THE PLAYFIELD
107$:	EOR CKC0
	STA NY,OBST		;NEW PLAYFIELD STAMP
	BNE 108$		;IF MUSHROOM DID NOT DISAPPEAR
	JSR MUSHDC		;DEC MUSHROOMS
108$:	JMP 20$			;ALWAYS

11$:	LDX I,13.
115$:	LDA X,MOBJP
	CMP I,76
	BCC 117$		;IF SEGMENT IS ALIVE
	CMP I,0B9
	BCC 132$		;IF SEGMENT IS DEAD
	CMP I,0F8
	BCS 132$		;IF SEGMENT IS DEAD
117$:	LDA X,MOBJV
	EOR CKF8
	CMP I,0F8
	BCS 132$		;IF OFF TOP OF SCREEN
	EOR CKF8
	SEC
	SBC SHOTV
	JSR ABS			;ABSOLUTE
	TAY
	CPX I,12.
	BNE 12$			;NOT ANT OR SCORPION
	LDA X,MOBJP
	EOR CKC0
	CMP I,20
	BCS 12$			;NOT ANT PICTURE
	LDA ANTDV
	EOR CKF8
	CMP I,4
	BCC 12$			;NOT FAST ANT
	CPY I,7			;SHOT GOES UP 6, FAST ANT DOWN 4
	BCS 132$		;NOT IN V WINDOW
	BCC 125$		;ALWAYS (WE HAVE V COLLISION)

12$:	CPY I,5			;SHOT UP 6, AND CENTS DOWN 2 (SELL AT 20)
	BCS 132$		;NOT IN V WINDOW
125$:	LDA X,MOBJH
	SEC
	SBC SHOTH
	JSR ABS			;ASSUMES SHOT IS IN MIDDLE OF EVERYTHING
	TAY
	CPX I,13.
	BEQ 13$			;IF SPIDER
	CPX I,12.
	BCC 142$		;IF CENTIPEDE
	LDA X,MOBJP
	EOR CKC0
	CMP I,20
	BCS 131$		;IF SCORPION
	CPY I,6			;CHECK ANT
	BCS 132$		;NOT IN H WINDOW
	LDY I,2			;200 POINTS FOR ANT
	LDA I,4
	CMP ANTDV
	BEQ 129$			;TIME TO EXPLODE ANT (2ND HIT)
	STA ANTDV		;SPEED UP ON FIRST HIT
	BNE 108$		;ALWAYS

131$:	CPY I,10.
	BCS 132$		;NOT IN H WINDOW
	LDY I,10		;1000 FOR SCORPION
129$:	JMP 18$			;ALWAYS


13$:	CPY I,10.
	BCC 137$			; IN H WINDOW
132$:	JMP 16$			;ALWAYS

137$:	LDY I,0B6
	STY PTS			;POINTS=300
	LDY I,3			;CALL FOR SCORING ROUTINE
	LDA BUGV
	SEC
	SBC PLAYV		;DISTANCE BETWEEN SPIDER AND PLAYER
	JSR ABS
	CMP I,40
	BCS 139$		;IF DISTANCE >=40
	INC PTS			;POINTS=900
	LDY I,9			;CALL FOR SCORING ROUTINE
	CMP I,16
	BCC 139$		;IF DISTANCE <16
	INC PTS			;POINTS=600
	LDY I,6			;CALL FOR SCORING ROUTINE
139$:	LDA I,80
	STA COUNT		;NEW SPIDER COUNTER
	STA COUNT2		;START COUNTER
	LDA I,0
	STA CHAN3		;TURN OFF SOUND
	LDA I,0F0
	CMP BUGH
	BCC 141$		;NOT OFF LEFT EDGE
	LDA I,10
	CMP BUGH
	BCC 18$			;OKAY ON RIGHT EDGE
141$:	STA BUGH		;KEEP PTS. DISPLAY ON SCREEN
	BNE 18$			;ALWAYS


142$:	CPY I,6
	BCS 16$			;NOT IN H WINDOW
	LDA I,0
	STA TEMP1		;MSB OF SCORE
	LDY I,10		;BODY=10 POINT
	LDA X,MOBJP
	AND I,40
	BNE 145$		;IF BODY SEGMENT
	LDY I,0			;HEAD=100 POINTS
	INC TEMP1
145$:	TYA
	JSR SCORNG
	CPX I,NCENT-1
	BEQ 15$			;END SEGMENT(LAST CENTI)
	LDA X,MOBJP+1
	BMI 15$			;END SEGMENT(NEXT DEAD)
	AND I,0BF
	STA X,MOBJP+1		;NEW HEAD
15$:	JSR CALLS
	JSR OBSTAC
	STX TEMP2		;SAVE X
	JSR MUSHER		;PUT MUSHROOMS ON SCREEN
	LDX TEMP2		;RESTORE X
	JMP 19$


16$:	DEX
	BMI 30$
	JMP 115$

18$:	STY TEMP1		;MSB OF SCORE
	LDA I,0			;LSB OF SCORE
	JSR SCORN1		;SCORES FOR SPIDER,ANT, OR SCORPION
19$:	LDA I,13
	STA CHAN0		;EXPLOSION SOUND
	LDA I,0FF
	STA X,MOBJP		;EXPLOSION PICTURE
20$:	JSR RSHOT		;POSITION SHOT
30$:	LDX PLAYER
	LDA X,DEAD-1
	ORA DELAY
	BEQ 50$			;IF ALL DEAD OR WE ARE DELAYING UNTIL NEXT WAVE
	LDA BUGP
	EOR CKC0
	CMP I,9C
	BCC 40$			;IF BUG NOT DEAD
	DEC COUNT
	BNE 40$			;SLOW DOWN NEW BUG
	JSR BUGOFF
40$:	RTS

50$:	INC X,CENTIS-1		;FASTER
	LDA I,40
	STA DELAY		;DELAY BEFORE NEXT WAVE
	RTS
	.PAGE
	.SBTTL SOUNDS-SOUND ROUTINE
;SOUNDS-CONROL SOUNDS FOR GAME
;
;CHAN 0=ALL EXPLOSIONS
;CHAN 1=BONUS, CENTIPEDE, ANT AND SCORPION SOUNDS
;CHAN 2=SHOT SOUND
;CHAN 3=SPIDER SOUND
SOUNDS:	LDX MODE
	BPL 5$			;IF NOT IN ATTRACT
	LDX I,0			;TURN OFF SOUNDS
	STX AUDC0
	STX AUDC1
	STX AUDC2
	STX AUDC3
	RTS
	
5$:	LDA FRAME
	LSR
	BCC 30$			;EVERY OTHER FRAME CHANGE BUG SOUND
	LDY CHAN3
	TYA
	BEQ 20$			;IF NO SOUND
	DEC CHAN3
	BNE 10$			;IF WE ARE NOT COMPLETING SOUND
	LDA I,14
	STA CHAN3		;RESET TO START OF SOUND
10$:	LDA Y,FREQ3-1
	STA AUDF3		;SET FRQUENCY
	LDA Y,CONT3-1
20$:	STA AUDC3		;SET CONTROL BYTE AND VOLUME
30$:	LDY CHAN2
	TYA
	BEQ 40$			;IF NO SHOT SOUND
	DEC CHAN2
	LDA Y,FREQ2-1
	STA AUDF2
	LDA I,64
40$:	STA AUDC2
	LDY CHAN4
	BEQ 48$			;IF NO BONUS SOUND
	LDA FRAME
	AND I,07
	BNE 52$			;CONTINUE LAST SOUND
	DEC CHAN4
	DEY
	BEQ 48$			;IF END OF SOUND
	LDA Y,FREQ4-1
	STA AUDF1
	LDA I,0A4
	BNE 50$			;ALWAYS

45$:	LDY CHAN6		;INDEX FOR
	DEY
	BNE 46$			;CONTINOUS LOOP
	LDY I,20.
46$:	STY CHAN6
	LDA Y,FREQ6-1
	STA AUDF1
	LDA I,0A4
	BNE 50$

48$:	LDA ANTV
	EOR CKF8
	CMP I,0F8
	BCS 49$			;IF ANT OR SCORPION OFF SCREEN
	LDA PLAYP
	AND I,0AF
	BNE 49$			;IF PLAYER IS EXPLODING OR DEAD
	LDA ANTP
	EOR CKC0
	CMP I,34
	BCS 49$			;IF ANT OR SCORPION IS EXPLODNG
	CMP I,20
	BCS 45$			;IF THE SCORPION
	LDA ANTV
	EOR CKFE
	LSR
	EOR I,0FF
	ORA I,80		;USE LOWER FREQUENCIES
	STA AUDF1
	LDA I,0A4
	BNE 50$			;ALWAYS

52$:	LDY CHAN0
	TYA
	BEQ 60$			;IF NO EXPLOSION SOUND
	DEC CHAN0
	LDA Y,FREQ0-1
	STA AUDF0
	LDA Y,CONT0-1
60$:	STA AUDC0
	RTS
49$:	LDY CHAN1
	TYA
	BEQ 50$			;IF CENTIPEDE SOUND
	DEC CHAN1
	LDA Y,FREQ1-1
	STA AUDF1
	LDA Y,CONT1-1
50$:	STA AUDC1
	LDY CHAN5
	BEQ 52$			;IF NO PLAYER EXPLOSION
	LDA FRAME
	AND I,3
	BNE 65$			;CONTINUE LAST SOUND
	DEC CHAN5
	DEY
	BEQ 65$			;IF END OF SOUND
	LDA Y,FREQ0-1
	STA AUDF0
	LDA Y,CONT0-1
	BEQ 64$			;LEAVE A DELAY BETWEEN EXPLOSIONS
	CLC
	ADC I,02		;INCREASE VOLUME
64$:	STA AUDC0
65$:	RTS

	
	
FREQ0:	.BYTE 0,0,0,0,0F0,0E0,0D0,0C0,0B0,0A0,90,80,70,60,50,40,30,20,10	;EXPLOSION SOUND
CONT0:	.BYTE 0,0,0,0,81,81,81,82,82,82,82,83,83,83,83,84,84,84,84
FREQ1:	.BYTE 70,0,0,0A0,0,0C0,0E0		;SOUND OF LITTLE FEET
CONT1:	.BYTE 0A1,0,0,0A2,0,0A2,0A4	;MUST BE REPEATED
FREQ2:	.BYTE 0F0,0E0,0D0,0C0,0B0,0A0,90,80,70,60,50		;SHOT SOUND
	
FREQ3:	.BYTE 05,05,20,20,30,30,35,35,30,30,20,20,05,05,20,20,30,30,35,35
CONT3:	.BYTE 0A1,0,0A2,0,0A3,0,0A4,0,0A3,0,0A2,0,0A1,0,0A2,0,0A3,0,0A2,0	;WELL REPEAT UNTIL TURNED OFF
FREQ4:	.BYTE 28,28,30,28,28,30,3C,51
	.BYTE 50,50,60,50,50,60,74,0A2,0
FREQ6:	.BYTE 60,60,70,70,60,60,60,70,70,70,50,50,80,80,50,50,50,80,80,80

	.SBTTL	SWAP-SWAPSCREENS
;SWAP-SWAP SCREENS IN TWO-PLAYER GAME
;
SWAP:	LDY I,0
	STY OBST
	LDA I,4
	STA OBST+1		;STARTING ADDRESS OF PLAYFIELD
	STY TEMP2		;TEMP2=INDEX INTO "STORE"
5$:	LDA I,0
	STA TEMP1
	STY TEMP2+1
	LDX I,8
10$:	LDA NY,OBST		;BUILD A BYTE FOR NEXT 8 PLAYFIELD LOCATIONS
	AND I,3F
	CMP I,38
	ROL TEMP1
	INY
	DEX
	BNE 10$
	LDX TEMP2
	LDA AX,STORE		;EXCHANGE CURRENT WITH OTHER PLAYERS "BYTE"
	INC TEMP2
	TAY
	LDA TEMP1
	STA AX,STORE
	STY TEMP1
	LDY TEMP2+1		;RESTORE Y
	LDX I,8
20$:	LDA I,0			;ADD NEXT PLYERS 8 PLAYFIELD STAMPS
	ROL TEMP1
	BCC 25$			;IF SPACE WAS EMPTY
	LDA I,3F		;INSERT MUSHROOM
	EOR CKC0
25$:	STA NY,OBST
	INY
	DEX
	BNE 20$
	TYA
	BNE 30$			;NO OVERFLOW
	INC OBST+1
30$:	CPY I,0C0
	BNE 5$			;MORE PLAYFIELD TO COPY
	LDA OBST+1
	CMP I,7
	BNE 5$			;STOP AT ADDRESS 7C0
	RTS
	.PAGE
	.SBTTL	TBLMT-LIMIT TRACKBALL MOVEMENT
;TBLMT-LIMIT TRACKBALL MOVEMENT
;
;EXIT (Y)=TRACK BALL READING(MSB)
;(A)=LSB'S OF TRACKBALL
TBLMT:	CMP I,08
	BCC 10$			;WITHIN RANGE
	CMP I,0F8
	BCS 10$			;WITHIN RANGE
	CMP I,80
	LDA I,08		;LIMIT RANGE TO 8
	BCC 10$
	LDA I,0F8		;LIMIT TO -8
10$:	CMP I,80		;SIGNED DIVIDE BY 2
	ROR
	TAY
	LDA I,0
	ROR			;SET LSB'S (0 OR 80)
	RTS
	.PAGE
	.SBTTL	UPDATE-UPDATE HIGH SCORE TABLE
;UPDATE-UPDATE HIGH SCORE TABLE

UPDATE:	LDA I,-1
	LDX I,1
	STA UPDFLG
	STA UPDFLG+1		;CLEAR FLAGS
	SED
	LDA GTIME		;ACCUMULATE GAME TIME
	CLC
	ADC EATIME
	STA EATIME
	LDA GTIME+1
	ADC EATIME+1
	STA EATIME+1
	LDA EATIME+2
	ADC I,0
	STA EATIME+2
	LDA EATIME+3
	ADC I,0
	BCS 5$			;DO NOT OVERFOW COUNTER
	STA EATIME+3
	LDA EAGAMS		;UPDATE NUMBER OF PLAYS
	CLC
	ADC NPLAYR
	STA EAGAMS
	LDA EAGAMS+1
	ADC I,0
	STA EAGAMS+1
	LDA EAGAMS+2
	ADC I,0
	STA EAGAMS+2		;IF THIS OVERFLOWS EA ROM IS DEAD
5$:	CLD
18$:	LDY I,0			;INDEX INTO HIGH SCORE TABLE
20$:	LDA Y,HSCORE
	CMP X,SCORE0		;SETS CARRY
	LDA Y,HSCORE+1
	SBC X,SCORE1
	LDA Y,HSCORE+2
	SBC X,SCORE2
	BCC 30$			;NEW HIGH SCORE
25$:	INY
	INY
	INY
	CPY I,3*NSCORE
	BCC 20$			;LOOP FOR ALL EXTRIES
28$:	DEX
	BPL 18$			;NEXT PLAYER
	LDA UPDFLG+1
	BMI 29$			;NO PLAYER 2 HIGH SCORE
	CMP UPDFLG
	BCC 29$			;IF PLAYER 2 SCORE IS BETTER THAN PLAYER 1
	ADC I,02		;ADD 3
	CMP I,3*NSCORE
	BCC 27$			;IF NOT OUT OF TABLE NOW
	LDA I,-1
27$:	STA UPDFLG+1		;NEW INDEX
29$:	LDA I,0
	STA UPDINT		;STARTING WITH FIRST INITIAL
	LDA UPDFLG+1
	AND UPDFLG
	BPL 90$			;IF ONE PLAYER MUST ENTER INITIALS TABLE
	LDA I,0
	STA FRAME+1		;DO NOT RESET SCREEN FOR 10 MINUTES
	JSR SCORES		;PUT UP HIGH SCORE TABLE
90$:	RTS

30$:	STX TEMP2		;SAVE INDEX
	STY TEMP2+1
	STY X,UPDFLG		;FLAG TO GET PLAYERS INITIALS
	LDX I,3*NSCORE-1
40$:	LDA X,INITL-3		;COPY INITIALS DOWN
	STA X,INITL
	LDA X,HSCORE-3
	STA X,HSCORE		;COPY HIGH SCORE
	DEX
	CPX TEMP2+1
	BNE 40$			;LOOP UNTIL ENTRIES MOVED UP
	LDA I,1
	STA X,INITL		;START LETTERS AT A
	LDA I,0			;CLEARS SECOND AND THIRD INITIALS
	STA X,INITL+1
	STA X,INITL+2
	STA TB			;CLEAR ANY EXTRA VALUES SO INITIALS START AT "A"
	LDX TEMP2		;LOOP FOR NEXT PLAYER
	LDA X,SCORE2		;MOVE HIGH SCORES IN
	STA Y,HSCORE+2
	LDA X,SCORE1
	STA Y,HSCORE+1
	LDA X,SCORE0
	STA Y,HSCORE
	LDA I,0F0		;1 MINUTE AT 60HZ
	STA FRAME+1		;PREPARE TO TIMEOUT GETTING INITIALS
	BNE 28$			;ALWAYS
	.PAGE
	.SBTTL UPSCRE-UPDATE SCORES ON SCREEN
;UPSCRE-UPDATE SCORES AT TOP OF SCREEN
;
UPSCRE:	LDA I,1F
	EOR CKBF
	STA TEMP4
	LDA I,4
	EOR CK03
	STA TEMP4+1		;PLAYFIELD ADDRESS
	LDA SCORE2
	SEC			;ZERO SUPPRESSION
	JSR DIGIT2
	LDA SCORE1
	JSR DIGIT2		;2 MORE DIGITS
	LDA SCORE0
	CLC			;NO ZERO SUPPRESSION
	JSR DIGIT2		;UPDATE PLAYER ONE'S SCORE
	LDY NPLAYR
	DEY
	BEQ 10$			;IF NOT 1 PLAYER
	LDA I,7
	EOR CK03
	STA TEMP4+1		;PLAYFIELD ADDRESS
	LDA I,1F		;INDEX FOR PLAYFIELD ADDRESS
	EOR CKBF
	STA TEMP4
	LDA SCORE2+1
	SEC
	JSR DIGIT2
	LDA SCORE1+1
	JSR DIGIT2		;2 MORE DIGITS
	LDA SCORE0+1
	CLC
	JSR DIGIT2		;UPDATE PLAYER TWO'S SCORE
10$:	LDA I,9F
	EOR CKBF
	STA TEMP4
	LDA I,5
	EOR CK03
	STA TEMP4+1
	LDA HSCORE+2
	SEC
	JSR DIGIT2		;DISPLAY 2 DIGITS OF HIGH SCORE
	LDA HSCORE+1
	JSR DIGIT2		;2 MORE DIGITS
	LDA HSCORE
	CLC
	JMP DIGIT2		;DISPLAY 2 DIGITS OF HIGH SCORE


	.REPT 3			;FILL REST OF MEMORY SPACE
	.BYTE 0FF
	.ENDR


CKSUM2:	.BYTE 90		;CHECKSUM 3000-37FF
	.END
	.PAGE
                                                                                                                                                                                                                                                                                                                                                                                                                                                                        