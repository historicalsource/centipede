	.TITLE CENIR4
	.NLIST
	.INCLUDE CENDE4
	.LIST
	.PAGE
	.CSECT	CENIRQ
INCLUD	=1			;THIS FILE IS INCLUDED
COIN67	=1			;COINS ARE ON D7,D6,D5
COIN	=0			;COIN SWITCHES ARE ACTIVE LOW
SLAM	=0			;SLAM SWITCH IS ACTIVE LOW
EMCTRS	=3			;3 COIN COUNTERS
BONADD	=1			;BONUS ADDERS ARE IMPLEMENTED
	.INCLUDE COIN65		;<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<

MESG:	.WORD MESS0
	.WORD MESS0G
	.WORD MESS0F
	.WORD MESS0S
	.WORD MESS1
	.WORD MESS1G
	.WORD MESS1F
	.WORD MESS1S
	.WORD MESS2
	.WORD MESS2G
	.WORD MESS2F
	.WORD MESS2S
	.WORD MESS3
	.WORD MESS3G
	.WORD MESS3F
	.WORD MESS3S
	.WORD MESS4
	.WORD MESS4G
	.WORD MESS4F
	.WORD MESS4S
	.WORD MESS5
	.WORD MESS5G
	.WORD MESS5F
	.WORD MESS5S
	.WORD MESS6
	.WORD MESS6G
	.WORD MESS6F
	.WORD MESS6S
	.WORD MESS7
	.WORD MESS7G
	.WORD MESS7F
	.WORD MESS7S
	.WORD MESS8
	.WORD MESS8G
	.WORD MESS8F
	.WORD MESS8S
	.WORD MESS9
	.WORD MESS9G
	.WORD MESS9F
	.WORD MESS9S
	.WORD MES10
	.WORD MES10G
	.WORD MES10F
	.WORD MES10S

	.MACRO	ASCIN A,B,C
	.WORD	A
	.WORD B
	.ASCIN	/C/
	.ENDM

MESS0:	ASCIN 56E,651,<PLAYER >
MESS0G:	ASCIN 56E,651,<SPIELER >
MESS0F:	ASCIN 56E,651,<JOUEUR >
MESS0S:	ASCIN 56E,651,<JUGADOR >
MESS1:	ASCIN 513,6AC,<1 COIN 2 PLAYS>
MESS1G:	ASCIN 4F3,6CC,<1 MUENZE 2 SPIELE>
MESS1F:	ASCIN 4F3,6CC,<1 PIECE 2 JOUEURS>
MESS1S:	ASCIN 4F3,6CC,<1 FICHA 2 JUEGOS>
MESS2:	ASCIN 513,6AC,<1 COIN 1 PLAY>
MESS2G:	ASCIN 4F3,6CC,<1 MUENZE 1 SPIEL>
MESS2F:	ASCIN 4F3,6CC,<1 PIECE 1 JOUEUR>
MESS2S:	ASCIN 4F3,6CC,<1 FICHA 1 JUEGO>
MESS3:	ASCIN 513,6AC,<2 COINS 1 PLAY>
MESS3G:	ASCIN 4F3,6CC,<2 MUENZEN 1 SPIEL>
MESS3F:	ASCIN 4F3,6CC,<2 PIECES 1 JOUEUR>
MESS3S:	ASCIN 4F3,6CC,<2 FICHAS 1 JUEGO>
MESS4:	ASCIN 56F,650,<GAME OVER>
MESS4G:	ASCIN 56F,650,<SPIELENDE>
MESS4F:	ASCIN 50F,6B0,<FIN DE PARTIE>
MESS4S:	ASCIN 4EF,6D0,<JUEGO TERMINADO>
MESS5:	ASCIN 4AB,714,<ENTER YOUR INITIALS>
MESS5G:	ASCIN 42B,794,<GEBEN SIE IHRE INITIALEN EIN>
MESS5F:	ASCIN 48B,734,<ENTREZ VOS INITIALES>
MESS5S:	ASCIN 48B,734,<ENTRE SUS INICIALES>
MESS6:	ASCIN 4F1,6CE,<BONUS EVERY >
MESS6G:	ASCIN 4F1,6CE,<BONUS JEDE >
MESS6F:	ASCIN 4D1,6EE,<BONUS CHAQUE >
MESS6S:	ASCIN 4F1,6CE,<EXTRA CADA >
MESS7:	ASCIN 55D,662,<HIGH SCORES>
MESS7G:	ASCIN 4DD,6E2,<HOECHSTERGEBNISSE>
MESS7F:	ASCIN 51D,6A2,<MEILLEURS SCORE>
MESS7S:	ASCIN 59D,622,<RECORDS>
MESS8:	ASCIN 52C,693,<GREAT SCORE>
MESS8G:	ASCIN 48C,733,<GROSSARTIGES ERGEBNIS>
MESS8F:	ASCIN 4EC,6D3,<SPLENDIDE SCORE>
MESS8S:	ASCIN 50C,6B3,<GRAN PUNTAJE>
MESS9:	ASCIN 552,66D,<CREDITS >
MESS9G:	ASCIN 552,66D,<KREDITE >
MESS9F:	ASCIN 552,66D,<CREDITS >
MESS9S:	ASCIN 552,66D,<CREDITOS >
MES10:	ASCIN 4F0,6CF,<2 CREDIT MINIMUM>
MES10G:	ASCIN 430,78F,<GELDEINWURF FUR 2 SPIELE>
MES10F:	ASCIN 4F0,6CF,<2 JUEX MINIMUM>
MES10S:	ASCIN 4F0,6CF,<2 JUEGAS MINIM0>
	.SBTTL	MESS-MESSAGE ROUTINE
;MESS-MESSAGE ROUTINE
;
;ENTRY  (A)=BITS 0-6 MESSAGE NUMBER (0,1,2,...)
;	    BIT 7=1 TO BLANK MESSAGE
;	    BIT 7=0 TO WRITE MESSAGE
;EXIT	(TEMP4,TEMP4+1)=ADDRESS OF DESTINATION
;USES	(TEMP1,TEMP1+1),(TEMP4,TEMP4+1),(TEMP5,TEMP5+1)
;CALLS	CHAR
MESS:	ASL
	ROR TEMP1+1		;SAVE BLANK/WRITE FLAG
	TAY
	ASL
	STA TEMP1
	LDA OPTNS
	AND I,03		;LANGUAGE BITS
	ORA TEMP1
	ASL
	TAX
	LDA X,MESG
	STA TEMP5
	LDA X,MESG+1
	STA TEMP5+1
	LDY I,0
	LDX CKC0
	BEQ 2$			;NOT COCKTAIL
	LDY I,2
2$:	LDA NY,TEMP5		;GET MESSAGE POSITION
	STA TEMP4
	INY
	LDA NY,TEMP5
	STA TEMP4+1
	LDY I,4			;PROPER POSITION OF INDEX
;***************************************
;
;THIS POINT ON DOWN SHOULD BE WITHIN THE
;LAST 2K (ONE 2716) OF THE PROGRAM SPACE.
;
;*****************************************
5$:	STY TEMP1
10$:	LDY TEMP1
	LDA NY,TEMP5
	AND I,3F		;GET RID OF SIGN BIT IF ANY
	CMP I,20
	BEQ 20$			;IF A BLANK, USE ZERO
	LDX TEMP1+1
	BPL 30$			;IF WRITE MESSAGE
20$:	LDA I,0			;BLANK MESSAGE
30$:	CMP I,30
	BCC 40$			;IF LETTER
	AND I,2F		;IF A NUMBER (MAP 31 TO 21, 32 TO 22,...)
40$:	JSR CHAR		;PUT OUT CHARACTER AND UPDATE (TEMP4,TEMP4+1)
45$:	LDY TEMP1
	INC TEMP1
	LDA NY,TEMP5
	BPL 10$			;IF MORE CHARACTERS TO COME
	RTS
	
MESSAG	=.
	LDY I,0	
	STY TEMP1+1		;DO NOT ERASE MESSAGE
	BEQ 5$			;ALWAYS

	.PAGE


	.SBTTL ABS/COMP
;ABS/COM-ABSOLUTE VALUE AND COMPLEMENT
;
;ENTRY	(A)=NUMBER
;	(CC)=NEGATIVE IF (A) IS NEGATIVE
;EXIT	(A)=RESULT
ABS:	BPL 10$			;IF POSITIVE
COMP	=.
	EOR I,0FF
	CLC
	ADC I,01
10$:	RTS



	.SBTTL	PRINT CHARACTER
;CHAR-PRINT ONE CHARACTER ON SCREEN
;ENTRY (TEMP4,TEMP4+1)=SCREEN POSITION
;      (A)=CHARACTER
;EXIT (TEMP4,TEMP4+1)=NEXT SCREEN POSITION
;
;INITIAL-DISPLAY A PLAYER'S INITIAL
;ENTRY(Y)=INDEX INTO INITL TABLE
;     (TEMP4,TEMP4+1=SCREEN POSITION
;EXIT (TEMP4,TEMP4+1)=NEXT SCREEN POSITION
;
INITAL:	LDA Y,INITL
CHAR:	TAY
	BEQ 10$			;LEAVE A BLANK AS 0
	EOR CKC0
10$:	LDY I,0
	STA NY,TEMP4
	LDA I,20
	EOR CKC0		;20 OR-20 EPENDNG ON COCKTAIL
	CLC
	ADC TEMP4
	STA TEMP4
	LDA CKFF
	ADC TEMP4+1
	STA TEMP4+1
	RTS


	.SBTTL DIGITS
;
;ENTRY (A)=2 DIGITS OF SCORE
;	(TEMP4)=ADDRESS AT WHICH SCORE IS TO BE DISLAYED
;	(CC)=CARRY CLEAR IF NO ZERO SUPPRESSION
;EXIT	(TEMP4,TEMP4+1)=NEXT SCREEN ADDRESS
;	(CC)=CARRY CLEAR IF NO ZERO SUPPRESSION
DIGIT2: PHA
	PHP
	LSR
	LSR
	LSR
	LSR
	PLP
	JSR DIGITZ		;PUT OUT A DIGIT
	PLA
	AND I,0F
DIGITZ:	BCC 10$		;IF NO ZERO SUPPRESSION
	AND I,0F
	BEQ 20$			;SUPPRESS THIS ZERO AND LEAVE CARRY SET
10$:	CLC
	ORA I,20		;DIGITS ARE 20-29
20$:	PHP
	CMP I,2A
	BCC 30$			;IF A NUMBER
	SBC I,29		;LETTERS START AT 1
30$:	JSR CHAR		;PUT OUT DIGIT
	PLP
	RTS

	.SBTTL IRQ-IRQ PROCESSING
IRQ:	PHA			;SAVE REGISTERS
	TXA
	PHA
	TYA
	PHA
	CLD			;CLEAR DECIMAL MODE
	LDA $LMTIM
	BEQ 4$			;IF NO ABUSE
	LDA I,10		;EAR PIERCING
	STA AUDF1
	LDA I,0AF		;CONSTANT TONE AND MAX VOLUME
	STA AUDC1
4$:	BIT ENDSCN
	BVS 5$			;WE ARE IN VBLANK
	JMP 50$

5$:	INC SYNC		;WE ARE IN NEXT FRAME
	INC FRAME		;UPDATE FRAME COUNTER
	BNE 8$			;IF NO OVERFLOW
	INC FRAME+1
	SED
	LDA GTIME		;INCREMENT GAME TIMER
	CLC
	ADC I,1
	STA GTIME
	LDA GTIME+1		;DO NOT WORRY ABOUT OVERFLOW
	ADC I,0			;40000 SECONDS=666 MINUTES=12 HOURS
	STA GTIME+1
	CLD
8$:	LDA SYNC
	CMP I,8
10$:	BCS 10$			;OOPS-WATCHDOG WILL RESET US.
	LDA $$CRDT
	CMP I,37.
15$:	BCS 15$			;OOPS-LET WATCHDOG RESET MS
	CMP I,19.
	BCC 20$			;IF SMALL # OF CREDITS
	LDA I,18.
	STA $$CRDT		;SET MAX NUMBER OF CREDITS
20$:	LDA JOYSTK		;CHECK JOYSTICKS
	LDX PLAYER
	LDY JHORIZ
	JSR JOYS		;RESPOND TO JOYSTICK
	STY JHORIZ		;NEW HORIZONTAL SPEED
	PHA
	TYA
	CLC
	ADC TB			;NEW HORIZONTAL SPEED
	STA TB
	PLA
	LDY JVERT
	JSR JOYS		;RESPOND TO JOYSTICK
	STY JVERT		;NEW VERTICAL SPEED
	TYA
	JSR COMP		;VERTICAL IS INVERTED
	CLC
	ADC TB+2		;NEW VERTICAL SPEED
	STA TB+2
	LDA X,LCOLOR-1		;LAST COLOR OF LEGS
	BMI 22$			;TIME TO CHANGE COLOR BETWEEN WAVES
	CMP I,40
	BCC 25$			;NOT TIME TO CHANGE PLAYERS IN 2-PLAYER GAME
	AND I,3F
	BPL 23$			;ALWAYS

22$:	AND I,3F
	CLC
	ADC I,03		;NEXT INDEX
	CMP I,42.
	BCC 23$			;IF NO OVERFOW
	LDA I,0
23$:	STA X,LCOLOR-1
	TAX
	JSR CLRCH		;CHANGE COLORS
25$:	LDX I,15.
30$:	LDA X,MOBJV		;SET MOTION OBJECT ATTRIBUTES
	STA X,VPOS		;SET VERTICAL POSITION
	LDA X,MOBJH
	LDY I,0			;LEAVE PICTURE ALONE IF SPIDER OR "100",ETC
	CPX I,13.
	BEQ 35$
	LDY X,MOBJDH
	BPL 35$			;IF FACING LEFT
	CLC
	ADC I,01		;FACE PICTURE BY CORRECTING HORIZONTAL
35$:	STA X,HPOS		;SET HORIZONTAL POSITION
	TYA
	AND I,80
	STA TPIRQ2
	LDA SLFTST
	AND I,20
	BNE 31$			;IF NOT IN SELF-TEST
	LDA X,MOBJP
	JMP 34$			;SKIP ALL PICTURE STUFF

31$:	LDA X,MOBJP
	CPX I,NCENT
	BCS 33$			;IF NOT A CENTEPEDE SEGMENT
	AND I,3F		;GET CORRECT PICTURE OF CONTIPEDE
	CMP I,30
	BCS 33$			;IF AN EXPLOSION
	AND I,0F		;REMOVE POISONED BIT
	STA TPIRQ1
	LDA X,MOBJV
	AND I,07
	BEQ 32$			;IF NOT TURNING
	TAY
	LDA I,08
	CPY I,06
	BCS 32$			;IF JUST STARTED TURNING
	CPY I,3
	BCC 32$			;IF ALMOST COMPLETED TURN
	LDA I,0C
32$:	EOR TPIRQ1		;FORM PICTURE CODE
33$:	EOR TPIRQ2
34$:	STA X,PICT		;SET PICUTRE #
	LDA X,MOBJP
	AND I,40		;GET HEAD/BODY FLAGS
	BEQ 36$			;IF A HEAD
	CPX I,NCENT
	BCS 36$			;IF NOT A CENTIPEDE SEGMENT
	LDA I,0C		;FILL IN EYES
36$:	ORA I,39		;SET INTENSITIVES FOR LEGS, HEAD AND EYES
38$:	STA X,COLOR
	DEX
	BPL 30$			;LOOP THRU ALL OBJECTS
50$:	LDA SLFTST
	AND I,20
	BNE 55$			;IF NOT SELF-TEST
	LDA COLCNT
	BMI 69$			;IF NOT IN COLOR BAR TEST
	INC COLCNT
	BIT ENDSCN
	BVC 52$			;IF NOT VBLANK
	LDA I,0
	STA COLCNT
52$:	LDA COLCNT
	ASL			;0,2,4 OR 6
	ASL			;0,4,8 OR 12.
	LDX I,03
53$:	STA X,COLORR		;SET UP COLOR REGISTERS
	ADC I,01		;CARRY WAS CLEAR AND IS LEFT CLEAR
	DEX
	BPL 53$
	BMI 69$			;ALWAYS-SKIP OVER COIN CHECK

55$:	JSR MOOLAH		;COIN ROUTINE
	LDX I,2
60$:	LDA X,$CCTIM		;SET COIN COUNTERS
	STA X,CCNTR
	DEX
	BPL 60$
	LDX I,10.		;NUMBER OF BYTES TO CHECKSUM
	LDA I,0F4		;EXPECTED CHECKSUM
65$:	EOR X,CPYRHT		;CHECKSUM THE COPYRIGHT MESSAGE
	DEX
	BPL 65$
	TAX
	BEQ 69$			;IF CHECKSUM IS OK
	TSX
	LDA I,3
	STA X,104		;SET FLAGS TO JUNK
69$:	LDX I,02
70$:	LDA X,TBALL		;READ TRACKBALL
	TAY
	SEC
	SBC X,OLDTB
	STY X,OLDTB		;SAVE CURRENT TRACK BALL READING
	AND I,0F		;LOWER 4 BITS ONLY
	CMP I,08
	BCC 85$			;SIGN EXTEND
	ORA I,0F0
85$:	TAY
	BEQ 89$			;PREVENTS STUCK TRACKBALL WHEN COUNTER WRAPPED
	EOR X,TBRD
	BPL 87$			;IF SAME DIRECTION AS LAST TIME
	TYA
	EOR X,TBALL
	BPL 87$			;IF WE ARE GOING IN THE CORRECT DIRECTION
	LDY X,TBRD		;USE LAST READING
87$:	TYA
	STA X,TBRD		;SAVE LAST READING
	CLC
	ADC X,TB
	STA X,TB		;UPDATE COUNT
89$:	DEX
	DEX
	BPL 70$			;ON TO NEXT TRACKBALL
90$:	STA IRQACK		;ACKNOWLEDGE IRQ
	PLA			;RESTORE REGISTERS
	TAY
	PLA
	TAX
	PLA
	RTI			;RETURN FROM IRQ


	.SBTTL	JOYS - READ AND RESPOND TO JOYSTICKS
;
; ENTRY	(Y) = LAST
; EXIT	(Y) = NEW Y DIRECTION
; USES	A,Y

JOYS:	ASL
	BCC 10$			;IF RIGHT OR DOWN
	ASL
	BCC 20$			;IF LEFT OR UP
	LDY I,0			;RESET Y DIRECTION
	RTS

10$:	CPY I,0FA
	BEQ 18$			;ALREADY AT MAX
	BCS 15$			;IF IT WAS GOING IN RIGHT DIRECTION
	LDY I,0			;RESET SPEED
15$:	DEY			;INCREASE SPEED
18$:	ASL			;REMOVE OTHER DIRECTION
	RTS

20$:	CPY I,06
	BEQ 28$			;ALREADY AT MAX
	BCC 25$			;IF IT WAS GOING IN RIGHT DIRECTION
	LDY I,0			;RESET SPEED
25$:	INY			;INCREASE SPEED
28$:	RTS

	.END
                                                                                                                                                                                                                                                                                                                                                                                                                                         